module Minilib.Net.Curl;

//import AsyncTask;
import Minilib.Common.Assert;
import Minilib.Text.StringEx;

type Curl = box struct {
    dtor: Destructor Ptr
};

// Creates a Curl object.
make: IOFail Curl;
make = (
    pure();;
    eval debug_eprintln("curl_glue_init");
    let glue = *FFI_CALL_IO[Ptr curl_glue_init()].lift;
    let curl = Curl {
        dtor: Destructor::make(glue, |glue|
            eval debug_eprintln("curl_glue_cleanup");
            if glue == nullptr { pure $ nullptr };
            FFI_CALL_IO[() curl_glue_cleanup(Ptr), glue];;
            pure $ nullptr
        )
    };
    pure $ curl
);

// `glue._check_error(io)` performs an IO monad and checks if any error occured.
_check_error: IO CInt -> Ptr -> IOFail ();
_check_error = |io_code, glue| (
    let code = *io_code.lift;
    if code != 0.to_CInt {
        let errmsg = *FFI_CALL_IO[Ptr curl_glue_get_error_message(Ptr), glue].lift;
        throw $ "curl: " + String::unsafe_from_c_str_ptr(errmsg)
    };
    pure()
);

// `curl._borrow_glue_iofail(|glue| iofail)` borrows the glue pointer and performs an IOFail monad.
_borrow_glue_iofail: (Ptr -> IOFail a) -> Curl -> IOFail a;
_borrow_glue_iofail = |f, curl| (
    curl.@dtor.borrow_io(|glue| f(glue).to_result).from_io_result
);

// Sets an URL of a Curl object.
set_url: String -> Curl -> IOFail ();
set_url = |url, curl| (
    pure();;
    curl._borrow_glue_iofail(|glue|
        url.borrow_c_str(|p_url|
            glue._check_error(FFI_CALL_IO[CInt curl_glue_set_url(Ptr, Ptr), glue, p_url])
        )
    )
);

// Sends an HTTP request and receives an HTTP response.
perform: Curl -> IOFail ();
perform = |curl| (
    pure();;
    curl._borrow_glue_iofail(|glue|
        glue._check_error(FFI_CALL_IO[CInt curl_glue_perform(Ptr), glue])
    )
);

// `curl._set_boxed_value(index, boxed)` sets an element of the internal array of boxed objects.
// `index` must be in the range: `0 <= index && index < MAX_BOXED_VALUES`.
// `boxed` will be marked as threaded.
_set_boxed_value: I64 -> Box a -> Curl -> IOFail ();
_set_boxed_value = |index, boxed, curl| (
    pure();;
    let boxed = mark_threaded(boxed);
    curl._borrow_glue_iofail(|glue|
        let retained_ptr = *FFI::boxed_to_retained_ptr(boxed).lift;
        let retain = FFI::get_funptr_retain(|_| boxed);
        let release = FFI::get_funptr_release(|_| boxed);
        FFI_CALL_IO[() curl_glue_set_boxed_value(Ptr, CInt, Ptr, Ptr, Ptr), glue, index.to_CInt, retained_ptr, retain, release].lift
    )
);

// `curl._get_boxed_value(index)` gets the element of the internal array of boxed objects.
_get_boxed_value: I64 -> Curl -> IOFail (Box a);
_get_boxed_value = |index, curl| (
    pure();;
    curl._borrow_glue_iofail(|glue|
        __get_boxed_value(index, glue).lift
    )
);

// `glue.__get_boxed_value(index)` gets the element of the internal array of boxed objects.
__get_boxed_value: I64 -> Ptr -> IO (Box a);
__get_boxed_value = |index, glue| (
    let retained_ptr = *FFI_CALL_IO[Ptr curl_glue_get_boxed_value(Ptr, CInt), glue, index.to_CInt];
    FFI::boxed_from_retained_ptr(retained_ptr)
);

// NOTE: This function is called from libcurl as a callback, so we can't use an IO monad as its return value.
//       So we have to use `unsafe_perform`.
_callback_write_function: Ptr -> CSizeT -> CSizeT -> Ptr -> CSizeT;
_callback_write_function = |ptr, size, nmemb, userdata| (
    do {
        let glue = userdata;
        //eval debug_eprintln("_callback_write_function: nmemb=" + nmemb.to_string);
        assert(|_| "invalid size", size == 1.to_CSizeT).lift;;
        assert(|_| "invalid nmemb", nmemb >= 0.to_CSizeT).lift;;

        let bytes: Array U8 = Array::fill(nmemb.to_I64, 0_U8);
        bytes.borrow_boxed_io(|p_bytes|
            FFI_CALL_IO[() memcpy(Ptr, Ptr, CSizeT), p_bytes, ptr, nmemb]
        ).lift;;

        let boxed: Box (Destructor WriteFunc) = *glue.__get_boxed_value(0).lift;
        boxed.@value.borrow_io(|write_func|
            write_func(bytes).map(to_CSizeT).to_result
        ).from_io_result
    }
    .try(|errmes| eprintln(errmes);; pure $ 0.to_CSizeT)
    .unsafe_perform
);
FFI_EXPORT[_callback_write_function, _callback_write_function];

type WriteFunc = Array U8 -> IOFail I64;

// Sets a callback function for writing to the Curl object.
set_write_callback: WriteFunc -> Curl -> IOFail ();
set_write_callback = |func, curl| (
    pure();;
    let boxed: Box (Destructor WriteFunc) = Box {
        value: Destructor::make(func, |func|
            eval debug_eprintln("write_func destructor is called");
            pure $ func
        )
    };
    curl._set_boxed_value(0, boxed);;
    curl._borrow_glue_iofail(|glue|
        glue._check_error(FFI_CALL_IO[CInt curl_glue_set_write_callback(Ptr), glue])
    )
);
