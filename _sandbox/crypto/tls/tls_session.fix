// Session for TLS 1.3
//
// RFC 8446 - The Transport Layer Security (TLS) Protocol Version 1.3
// https://tex2e.github.io/rfc-translater/html/rfc8446.html
module Minilib.Crypto.Tls.TLSSession;

import AsyncTask;
import Debug;

import Minilib.Crypto.Cert.CACertificates;
import Minilib.Crypto.Cert.X509Time;
import Minilib.Crypto.Tls.Types;
import Minilib.Crypto.Tls.HandshakeState;
import Minilib.Crypto.Tls.TLSSocket;
import Minilib.Crypto.SecureRandom;
import Minilib.Crypto.Cipher.CipherApi;
//import Minilib.Crypto.Cipher.CipherApiDefault;
import Minilib.Crypto.Cipher.CipherApiMock;
import Minilib.Monad.BytesIO;
import Minilib.Monad.Random;
import Minilib.Monad.State;
import Minilib.Monad.Trans;
import Minilib.Monad.Error;
import Minilib.Net.Tcp;
import Minilib.Text.Hex;
import Minilib.Text.StringEx;

type TLSSession = unbox struct {
    socket: TLSSocket,
    var_secure_random: Var SecureRandom,
    ca_certs: CACertificates,
    handshake_state: HandshakeState,
};

impl TLSSession: GetRandomSource {
    type RandomGeneratorType TLSSession = SecureRandom;
    get_random_generator = @var_secure_random;
}

impl TLSSession: GetByteIO {
    type BytesIOType TLSSession = TLSSocket;
    get_bytes_io = TLSSession::@socket;
}

type StateTLSSession = StateT TLSSession IOFail;

namespace TLSSession {
    // Creates a TLSSession object.
    make: TLSSocket -> IOFail TLSSession;
    make = |socket| (
        let secure_random = *SecureRandom::make;
        let var_secure_random = *Var::make(secure_random).lift;
        let ca_certs = *CACertificates::make;
        let handshake_state = *HandshakeState::make;
        pure $ TLSSession {
            socket: socket,
            var_secure_random: var_secure_random,
            ca_certs: ca_certs,
            handshake_state: handshake_state,
        }
    );

    // Gets the current handshake state.
    get_hstate: StateTLSSession HandshakeState;
    get_hstate = get_state.map(@handshake_state);

    // Sets the current handshake state.
    set_hstate: HandshakeState -> StateTLSSession ();
    set_hstate = |hstate| mod_state(set_handshake_state(hstate));

    lift_hstate: (HandshakeState -> StateTLSSession HandshakeState) -> StateTLSSession ();
    lift_hstate = |f| (
        let hstate = *get_hstate;
        let hstate = *f(hstate);
        set_hstate(hstate)
    );

    act_hstate: (HandshakeState -> StateTLSSession (a, HandshakeState)) -> StateTLSSession a;
    act_hstate = |f| (
        let hstate = *get_hstate;
        let (a, hstate) = *f(hstate);
        eval *set_hstate(hstate);
        pure $ a
    );

    // Establishes a handshake.
    // Specifically, it sends a ClientHello message, and receives ServerHello,
    // EncryptedExtensions, Certificate, CertificateVerify, Finished messages.
    // Then it sends a Finished message.
    establish_handshake: String -> StateTLSSession ();
    establish_handshake = |server_name| (
        eval *_send_client_hello(server_name);
        eval *_recv_server_hello;
        eval *_recv_server_params;
        pure()
    );

    _send_client_hello: String -> StateTLSSession ();
    _send_client_hello = |server_name| (
        let session = *get_state;
        let now = *X509Time::get_now.lift.lift_t;
        //let now = *from_generalized_time_string("20991231000000Z").from_result_t;
        let cipher_api = *make_cipher_api.from_result_t;
        let init_param = HandshakeInitParam {
            cipher_api: cipher_api,
            server_name: server_name,
            ca_certs: some $ session.@ca_certs,
            validate_cert_date: some $ now,
        };
        eval *HandshakeState::start_handshake(init_param).lift_hstate;
        eval *HandshakeState::send_records.lift_hstate;
        pure()
    );

    _recv_server_hello: StateTLSSession ();
    _recv_server_hello = (
        eval *HandshakeState::recv_record.lift_hstate;
        pure()
    );

    _recv_server_params: StateTLSSession ();
    _recv_server_params = (
        eval *HandshakeState::recv_record.lift_hstate;
        if !(*get_hstate).is_after_finished {
            _recv_server_params  // try again
        };
        eval *HandshakeState::send_records.lift_hstate;
        pure()
    );

    // Sends an application data.
    send_appdata: Array U8 -> StateTLSSession ();
    send_appdata = |appdata| (
        eval *HandshakeState::send_appdata(appdata).lift_hstate;
        eval *HandshakeState::send_records.lift_hstate;
        pure()
    );

    // Receives an application data.
    recv_appdata: StateTLSSession (Array U8);
    recv_appdata = (
        eval *HandshakeState::send_records.lift_hstate;
        eval *HandshakeState::recv_record.lift_hstate;
        let appdata = *HandshakeState::pop_appdata_received.act_hstate;
        if appdata.is_empty {
            // try again
            recv_appdata
        };
        pure $ appdata
    );
}
