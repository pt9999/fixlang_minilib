// Duality test for Writer monad and Traced comonad
module Main;

import Minilib.Common.Common;
import Minilib.Comonad.Env;
import Minilib.Comonad.Store;
import Minilib.Comonad.Traced;
import Minilib.Monad.Error;
import Minilib.Monad.Reader;
import Minilib.Monad.State;
import Minilib.Monad.Writer;
import Minilib.Trait.Comonad;
import Minilib.Trait.Dual;

test_reader_env_zap: IO ();
test_reader_env_zap = (
    println("=== test_reader_env_zap ===");;
    let reader: Reader String I64 = do {
        let str = *ask;
        let i: I64 = str.from_string.catch(|errmsg| ok $ 0).as_ok;
        pure $ i * 2
    };
    let env: Env String String = env("42", "hello");
    let (i64, str) = zap(pair, reader, env);
    println("(i64, str)=" + (i64, str).to_string);;
    let (str, i64) = zap(pair, env, reader);
    println("(str, i64)=" + (str, i64).to_string)
);

test_state_store_zap: IO ();
test_state_store_zap = (
    println("=== test_state_store_zap ===");;
    let state: State I64 String = do {
        let i = *get_state;
        let i = i * 2;
        put_state(i);;
        pure $ "state i=" + i.to_string
    };
    let store: Store I64 String = store $ (42, |i| "store i=" + i.to_string);
    let (str1, str2) = zap(pair, state, store);
    println("(str1, str2)=" + (str1, str2).to_string);;
    let (str2, str1) = zap(pair, store, state);
    println("(str2, str1)=" + (str2, str1).to_string)
);

test_writer_traced_zap: IO ();
test_writer_traced_zap = (
    println("=== test_writer_traced_zap ===");;
    let writer: Writer String I64 = do {
        tell("Hello ");;
        tell("World!");;
        pure $ 42
    };
    let traced: Traced String String = traced $ id;
    let (i64, str) = zap(pair, writer, traced);
    println("(i64, str)=" + (i64, str).to_string);;
    let (str, i64) = zap(pair, traced, writer);
    println("(str, i64)=" + (str, i64).to_string)
);

main: IO ();
main = (
    test_reader_env_zap;;
    test_state_store_zap;;
    test_writer_traced_zap
);
