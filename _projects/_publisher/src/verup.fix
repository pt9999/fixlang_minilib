module Publisher.Verup;

import RegExp;
//import Minilib.IO.Errno;
//import Minilib.IO.FileSystem;
//import Minilib.IO.Path;
import Minilib.Monad.Error;
//import Minilib.Monad.IO;
import Minilib.Text.StringEx;


increment_prerelease: String -> Result ErrMsg String;
increment_prerelease = |prerelease| (
    let regexp = *RegExp::compile("^(.*\\D)?(\\d+)?$", "");
    let groups = *regexp.match_one(prerelease).catch(|errmsg|
        error $ "invalid prerelease format: " + prerelease
    );
    let prefix = groups.@(1);
    let suffix = groups.@(2);
    let suffix: I64 = *if suffix != "" { from_string(suffix) } else { pure $ 1 };
    let suffix = (suffix + 1).to_string;
    pure $ prefix + suffix
);

increment_version: String -> Result ErrMsg String;
increment_version = |version| (
    let regexp = *RegExp::compile("^(\\d+.\\d+.\\d+)(-[-0-9A-Za-z]+)?(\\+[-0-9A-Za-z]+)?$", "");
    let groups = *regexp.match_one(version).catch(|errmsg|
        error $ "invalid version format: " + version
    );
    let triplets = groups.@(1);
    let prerelease = groups.@(2);
    let metadata = groups.@(3);
    let triplets = triplets.split(".").take(3).to_array;
    let major = triplets.@(0);
    let minor = triplets.@(1);
    let patch = triplets.@(2);
    let (patch, prerelease) = *if prerelease == "" {
        let patch = (*from_string(patch) + 1).to_string;
        pure $ (patch, prerelease)
    } else {
        let prerelease = *increment_prerelease(prerelease);
        pure $ (patch, prerelease)
    };
    let triplets = major + "." + minor + "." + patch;
    pure $ triplets + prerelease + metadata
);
