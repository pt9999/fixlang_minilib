module Json;

import Debug;

import StringEx;
import OrderedMap;

type Json = box union {
    null: (),
    bool: Bool,
    number: F64,
    string: String,
    object: OrderedMap String Json,
    array:  Array Json
};

to_object: Array (String, Json) -> Json;
to_object = |arr| (
    let map = arr.to_iter.fold(
        OrderedMap::empty(arr.get_size),
        |h, (k, v)| h.insert(k, v)
    );
    object $ map
);

impl Json: Eq {
    eq = |j1, j2| (
        //eval debug_eprintln("eq");
        if j1.is_null && j2.is_null {
            true
        };
        if j1.is_bool && j2.is_bool {
            j1.as_bool == j2.as_bool
        };
        if j1.is_number && j2.is_number {
            j1.as_number == j2.as_number
        };
        if j1.is_string && j2.is_string {
            j1.as_string == j2.as_string
        };
        if j1.is_object && j2.is_object {
            j1.as_object == j2.as_object
        };
        if j1.is_array && j2.is_array {
            j1.as_array == j2.as_array
        };
        false
    );
}

_DOUBLEQUOTE: U8;
_DOUBLEQUOTE = 34_U8; // '\"'

_escape_from: Array U8;
_escape_from = "\"\\\n\r\t".get_bytes.pop_back;

_escape_to: Array U8;
_escape_to = "\"\\nrt".get_bytes.pop_back;

_escape_table: Array U8;
_escape_table = (
    Iterator::count_up(0).take(256)
    .map(|i| 
        _escape_from.find_by(|x| x == i.to_U8)
        .map_or(0_U8,
            |pos| _escape_to.@(pos)
        )
    )
    .to_array
);

_unescape_from: Array U8;
_unescape_from = "\"\\/bfnrt".get_bytes.pop_back;

_unescape_to: Array U8;
_unescape_to = "\"\\/\u0008\u000c\n\r\t".get_bytes.pop_back;

_unescape_table: Array U8;
_unescape_table = (
    Iterator::count_up(0).take(256)
    .map(|i| 
        _unescape_from.find_by(|x| x == i.to_U8)
        .map_or(0_U8,
            |pos| _unescape_to.@(pos)
        )
    )
    .to_array
);

_hex_table: Array U8;
_hex_table = (
    Iterator::count_up(0).take(16)
    .map(|i|
        if i < 10 { /* '0' = */ 48_U8 + i.to_U8 } 
        else { /* 'a' = */ 97_U8 + (i - 10).to_U8 }
    )
    .to_array
);
