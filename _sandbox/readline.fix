/*
    以下のように実行する。
    $ fix run -d readline -f readline.fix

    あらかじめ libreadline-dev をインストールしておく必要がある。
    $ sudo apt install libreadline-dev
*/
module Main;

_EOF: String;
_EOF = "EOF";

readline: String -> IOFail String;
readline = |prompt| (
    let ptr = prompt.borrow_c_str(|p_prompt|
        CALL_C[Ptr readline(Ptr), p_prompt]
    );
    if ptr == nullptr {
        throw $ _EOF
    };
    let str = String::_unsafe_from_c_str_ptr(ptr);
    let _ = if str.get_size > 0 {
        CALL_C[() add_history(Ptr), ptr]
    } else {
        ()
    };
    let _ = CALL_C[() free(Ptr), ptr];
    pure $ str
);

main: IO ();
main = (
    do {
        loop_m(
            (), |_|
            let str = *readline("$ ");
            let _ = *println ("--> " + str).lift;
            continue_m $ ()
        )

    }.try(eprintln)
);
