module Main;

import Minilib.Common.Common;
import Minilib.Common.IORef;
import Minilib.Common.TimeEx;
import Minilib.Monad.Free;
import Minilib.Monad.IO;
import Minilib.Monad.Error;
import Minilib.Monad.State;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;

test_free_lift_f: TestCase;
test_free_lift_f = (
    make_test("test_free_lift_f") $ |_|
    let free: Free Array I64 = [123, 456].lift_f;
    assert_true("free.is_f_functor", free.is_f_functor);;
    let arr: Array (Free Array I64) = free.as_f_functor;
    assert_equal("arr.@size", 2, arr.@size);;
    assert_true("arr.@(0).is_f_pure", arr.@(0).is_f_pure);;
    assert_true("arr.@(1).is_f_pure", arr.@(1).is_f_pure);;
    assert_equal("arr.@(0).as_f_pure", 123, arr.@(0).as_f_pure);;
    assert_equal("arr.@(1).as_f_pure", 456, arr.@(1).as_f_pure);;
    pure()
);

test_free_functor: TestCase;
test_free_functor = (
    make_test("test_free_functor") $ |_|
    let free: Free Option I64 = some(123).lift_f;
    let free: Free Option String = free.map(to_string);
    assert_true("is_f_functor", free.is_f_functor);;
    let opt: Option (Free Option String) = free.as_f_functor;
    assert_true("opt.is_some", opt.is_some);;
    assert_true("opt.as_some.is_f_pure", opt.as_some.is_f_pure);;
    assert_equal("opt.as_some.as_f_pure", "123", opt.as_some.as_f_pure);;
    pure()
);

test_free_monad: TestCase;
test_free_monad = (
    make_test("test_free_monad") $ |_|
    let free: Free Option I64 = pure(123);
    assert_true("free.is_f_pure", free.is_f_pure);;
    assert_equal("free.as_f_pure", 123, free.as_f_pure);;
    let free2: Free Option String = free.bind(|a| some(a.to_string).lift_f);
    assert_true("free2.is_f_functor", free2.is_f_functor);;
    let opt: Option (Free Option String) = free2.as_f_functor;
    assert_true("opt.is_some", opt.is_some);;
    assert_true("opt.as_some.is_f_pure", opt.as_some.is_f_pure);;
    assert_equal("opt.as_some.as_f_pure", "123", opt.as_some.as_f_pure);;
    pure()
);

type Op a = box union {
    op_iof: IOFail a,
    op_read: ((), Option String -> a),
    op_write: (String, () -> a),
};

impl Op: Functor {
    map = |f, op| (
        match op {
            op_iof(iof) => op_iof(iof.map(f)),
            op_read((arg, next)) => op_read((arg, next >> f)),
            op_write((arg, next)) => op_write((arg, next >> f)),
        }
    );
}

impl Op: MonadIOIF {
    lift_io = |io| lift_iofail(io.lift);
}

impl Op: MonadIOFailIF {
    lift_iofail = |iof| op_iof(iof);
}

type FreeOp a = Free Op a;

namespace FreeOp {
    fop_read: FreeOp (Option String);
    fop_read = op_read(((), id)).lift_f;

    fop_write: String -> FreeOp ();
    fop_write = |str| op_write((str, id)).lift_f;
}

type MockState = unbox struct {
    inputs: Array String,
    outputs: Array String,
};

namespace State {
    act_state_t: [m: Monad] (s -> (a, s)) -> StateT s m a;
    act_state_t = |f| make_state_t_monad $ f >> swap >> pure;
}

namespace MockState {
    make: Array String -> MockState;
    make = |inputs| MockState {
        inputs: inputs.reverse,
        outputs: []
    };

    get_outputs: MockState -> Array String;
    get_outputs = @outputs;

    interpret: FreeOp a -> StateT MockState IOFail a;
    interpret = |free| (
        match free {
            f_pure(a) => pure $ a,
            f_functor(fa) => match fa {
                op_iof(iof) => (
                    let next = *iof.lift_iofail; 
                    next.interpret
                ),
                op_read(((), next)) => (
                    let input = *act_state_t(act_inputs(|inputs|
                        match inputs.get_last {
                            none() => (none(), inputs),
                            some(input) => (some(input), inputs.pop_back)
                        }
                    ));
                    next(input).interpret
                ),
                op_write((str, next)) => (
                    State::mod_state(mod_outputs(push_back(str)));;
                    next().interpret
                ),
            }
        }
    );
}

test_freeop: TestCase;
test_freeop = (
    make_test("test_freeop") $ |_|
    let ioref: IORef (Array I64) = *IORef::make([]);
    let freeop1: FreeOp () = do {
        loop_m(
            0, |i|
            match *fop_read {
                none() => break_m $ (),
                some(input) => (
                    fop_write("got " + input);;
                    ioref.mod(push_back(i));;
                    continue_m $ i + 1
                )
            }
        )
    };
    let state = MockState::make(["hello", "world"]);
    let state = *freeop1.interpret.exec_state_t(state);
    assert_equal("outputs", ["got hello", "got world"], state.get_outputs);;
    assert_equal("ioref", [0, 1], *ioref.get);;
    pure()
);

test_freeop_perf: TestCase;
test_freeop_perf = (
    make_test("test_freeop_perf") $ |_|
    let ioref: IORef (Array String) = *IORef::make([]);
    let freeop: FreeOp () = do {
        loop_m(
            (), |_|
            match *fop_read {
                none() => break_m $ (),
                some(input) => (
                    fop_write(input);;
                    ioref.mod(push_back(input));;
                    continue_m $ ()
                )
            }
        )
    };
    let n = 100000;
    let state = MockState::make(Array::fill(n, ""));
    let (res, time) = *consumed_realtime_while_io(
        freeop.interpret.exec_state_t(state).to_result
    );
    let state = *res.from_result;
    emprintln((n, time).format("test_freeop_perf: n={} time={} seconds"));;
    assert_equal("outputs", n, state.get_outputs.get_size);;
    assert_equal("ioref", n, (*ioref.get).get_size);;
    pure()
);

main: IO ();
main = (
    [
        test_free_lift_f,
        test_free_functor,
        test_free_monad,
        test_freeop,
        test_freeop_perf,
    ]
    .run_test_driver
);
