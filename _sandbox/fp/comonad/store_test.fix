module Main;

import Minilib.Comonad.Store;
import Minilib.Trait.Comonad;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;

test_store_get: TestCase;
test_store_get = (
    make_test("test_store_get") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    assert_equal("get", (5, 5 * 5), wa.get)
);

test_store_pos: TestCase;
test_store_pos = (
    make_test("test_store_pos") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    assert_equal("pos", 5, wa.pos)
);

test_store_seek: TestCase;
test_store_seek = (
    make_test("test_store_seek") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let wa = wa.seek(10);
    assert_equal("get", (10, 10 * 10), wa.get)
);

test_store_seeks: TestCase;
test_store_seeks = (
    make_test("test_store_seeks") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let wa = wa.seeks(add(2));
    assert_equal("get", (7, 7 * 7), wa.get)
);

test_store_peek: TestCase;
test_store_peek = (
    make_test("test_store_peek") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let a = wa.peek(8);
    assert_equal("peek", 8 * 8, a)
);

test_store_peeks: TestCase;
test_store_peeks = (
    make_test("test_store_peeks") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let a = wa.peeks(add(-2));
    assert_equal("peeks", 3 * 3, a)
);

test_store_experiment: TestCase;
test_store_experiment = (
    make_test("test_store_experiment") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let g = |p| range(p - 2, p + 3).to_array;
    let arr = wa.experiment(g);
    assert_equal("experiment", [3 * 3, 4 * 4, 5 * 5, 6 * 6, 7 * 7], arr)
);

test_store_duplicate: TestCase;
test_store_duplicate = (
    make_test("test_store_duplicate") $ |_|
    let wa: Store I64 I64 = store $ (5, |s| s * s);
    let wa: Store I64 (Store I64 I64) = wa.duplicate;
    assert_equal("peek", (9, 9 * 9), wa.peek(9).get)
);

test_store_functor: TestCase;
test_store_functor = (
    make_test("test_store_functor") $ |_|
    let f: F64 -> F64 = |x| x * (1.0 - x);
    let wa = store $ (0.2, f);
    let wa = wa.map(add(0.1));
    let wa = wa.map(to_string);
    assert_equal("pos", 0.2, wa.pos);;
    let expected = (0.2 * (1.0 - 0.2) + 0.1).to_string;
    assert_equal("extract", expected, wa.extract)
);

test_store_comonad: TestCase;
test_store_comonad = (
    make_test("test_store_comonad") $ |_|
    let w: Store I64 I64 = store $ (2, |s| s * s);
    assert_equal("extract", 2 * 2, w.extract);;
    assert_equal("peeks(add(1))", 3 * 3, w.peeks(add(1)));;
    assert_equal("peeks(add(-1))", 1 * 1, w.peeks(add(-1)));;
    let w: Store I64 I64 = w.extend(|w| w.peeks(add(0)) + w.peeks(add(1)));
    assert_equal("extract", 2 * 2 + 3 * 3, w.extract);;
    assert_equal("peeks(add(1))", 3 * 3 + 4 * 4, w.peeks(add(1)));;
    assert_equal("peeks(add(-1))", 1 * 1 + 2 * 2, w.peeks(add(-1)));;
    pure()
);

test_store_comonad_laws: TestCase;
test_store_comonad_laws = (
    make_test("test_store_comonad_laws") $ |_|
    let w: Store I64 I64 = store $ (2, |s| s * s);
    let f: Store I64 I64 -> U64 = |w| (w.extract + 10).to_U64;
    let g: Store I64 U64 -> U32 = |w| (w.extract + 20_U64).to_U32;
    let h: I64 -> String = to_string;
    assert_equal("extend(extract) == id", w.get, w.extend(extract).get);;
    assert_equal("extend(f).extract == f", f(w), w.extend(f).extract);;
    assert_equal("extend(f).extend(g) == extend(extend(f) >> g)", w.extend(extend(f) >> g).get, w.extend(f).extend(g).get);;
    assert_equal("duplicate.extract == id", w.get, w.duplicate.extract.get);;
    assert_equal("duplicate.map(extract) == id", w.get, w.duplicate.map(extract).get);;
    assert_equal("extend(f) == duplicate.map(f)", w.duplicate.map(f).get, w.extend(f).get);;
    assert_equal("map(h) == extend(extract >> h)", w.map(h).get, w.extend(extract >> h).get);;
    pure()
);

main: IO ();
main = (
    [
        test_store_get,
        test_store_pos,
        test_store_seek,
        test_store_seeks,
        test_store_peek,
        test_store_peeks,
        test_store_experiment,
        test_store_duplicate,
        test_store_functor,
        test_store_comonad,
        test_store_comonad_laws,
        TestCase::empty
    ]
    .run_test_driver
);


