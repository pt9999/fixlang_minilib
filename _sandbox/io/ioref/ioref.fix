// A mutable reference whose value can be changed using the IO monad.
module Minilib.Common.IORef;

// A mutable reference whose value can be changed using the IO monad.
//
// NOTE: `IORef` is not a thread-safe. If you want to require thread-safety, use `Var`.
type IORef a = unbox struct {
    dtor: Destructor Ptr
};

namespace IORef {
    // Creates a mutable reference with the specified initial value.
    //
    // # Parameters
    // - `a`: an initial value
    make: a -> IO (IORef a);
    make = |a| (
        let box_a: Box a = Box::make(a);
        let retained_ptr = *box_a.boxed_to_retained_ptr;    // +1
        let f: Lazy (Box a) = |_| undefined("");
        let retain = get_funptr_retain(f);
        let release = get_funptr_release(f);
        let ior = *FFI_CALL_IO[Ptr minilib_ioref_init(Ptr, Ptr, Ptr), retained_ptr, retain, release];
        pure $ IORef {
            dtor: *Destructor::make(ior, |ior|
                if ior == nullptr { pure $ nullptr };
                FFI_CALL_IO[() minilib_ioref_cleanup(Ptr), ior];;   // -1
                pure $ nullptr
            )
        }
    );

    // Gets the value of a mutable reference.
    //
    // # Parameters
    // - `ioref`: a mutable reference
    get: IORef a -> IO a;
    get = |ioref| (
        ioref.@dtor.borrow_io(|ior|
            let retained_ptr = *FFI_CALL_IO[Ptr minilib_ioref_get_retained_ptr(Ptr), ior];
            FFI_CALL_IO[() minilib_ioref_retain(Ptr), ior];;    // +1
            let box_a: Box a = *retained_ptr.boxed_from_retained_ptr;    // -1
            pure $ box_a.@value
        )
    );

    // Sets the value of a mutable reference.
    //
    // # Parameters
    // - `a`: a value
    // - `ioref`: a mutable reference
    set: a -> IORef a -> IO ();
    set = |a, ioref| (
        ioref.mod(|_| a)
    );

    // `put` is a synonym of `set`.
    put: a -> IORef a -> IO () = IORef::set;

    // Modifies the value of a mutable reference.
    //
    // # Parameters
    // - `f`: a function for modifying the value
    // - `ioref`: a mutable reference
    mod: (a -> a) -> IORef a -> IO ();
    mod = |f, ioref| (
        ioref.@dtor.borrow_io(|ior|
            let retained_ptr = *FFI_CALL_IO[Ptr minilib_ioref_get_retained_ptr(Ptr), ior];
            let box_a: Box a = *retained_ptr.boxed_from_retained_ptr;    // -1
            let a = box_a.@value;
            let a = f(a);
            let box_a: Box a = Box::make(a);
            let retained_ptr = *box_a.boxed_to_retained_ptr;    // +1
            FFI_CALL_IO[() minilib_ioref_set_retained_ptr(Ptr, Ptr), ior, retained_ptr]
        )
    );
}
