module Minilib.Monad.Error;

// A trait for the interface of monads which can report errors.
trait MonadError = Monad + MonadErrorIF;

// A trait for monads which can report errors.
trait [m: * -> *] m: MonadErrorIF {
    // The type of error.
    type ErrorType m;

    // `error(e)` throws an error.
    error: ErrorType m -> m a;

    // `ma.catch(handler)` catches any error that is thrown during the computation of `ma`.
    catch: (ErrorType m -> m a) -> m a -> m a;
}

impl Result e: MonadErrorIF {
    type ErrorType (Result e) = e;
    error = Result::err;
    catch = |handler, res| (
        if res.is_err { handler(res.as_err) };
        res
    );
}

impl IOFail: MonadErrorIF {
    type ErrorType IOFail = ErrMsg;
    error = IOFail::throw;
    catch = |handler, iof| (
        let res = *iof.to_result.lift;
        if res.is_err { handler(res.as_err) };
        res.from_result
    );
}

