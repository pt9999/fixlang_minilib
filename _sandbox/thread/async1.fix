module Main;

import Subprocess;
import AsyncTask;

import Minilib.Trait.Traversable;

namespace ExitStatus {
    _to_string: ExitStatus -> String;
    _to_string = |s| (
        if s.is_exit { "exit(" + s.as_exit.to_string + ")" };
        if s.is_signaled { "signaled(" + s.as_signaled.to_string + ")" };
        if s.is_wait_failed { "wait_failed()" };
        ""
    );
}

is_success: ExitStatus -> Bool;
is_success = |s| (
    s.is_exit && s.as_exit == 0_U8
);

run_command: Array String -> IOFail String;
run_command = |argv| (
    let ((output, error), exit_status) = *(Subprocess::run_string (argv.@(0), argv, ""));
    eval *(
        if error != "" { lift $ eprintln(error) } else { pure() }
    );
    if !exit_status.is_success {
        throw $ "exit_status = " + exit_status._to_string
    };
    pure $ output
);

find_files: () -> IOFail (Array String);
find_files = |_| (
    let argv = [
        "/usr/bin/find", ".", "-name", ".fixlang", "-prune", "-o",
        "-name", "*.fix", "-print"
    ];
    let output = *(run_command (argv));
    let files = output.split("\n").filter(|s| s != "").to_array;
    pure $ files
);

with_input_from_file: Path -> (IOHandle -> IOFail a) -> IOFail a;
with_input_from_file = |file_path, f| (
    let file_handle = *(open_file(file_path, "rt"));
    let res = *(lift $ to_result $ f (file_handle));
    lift $ close_file(file_handle);;
    if res.is_err {
        throw $ res.as_err
    };
    pure $ res.as_ok
);

namespace FileInfo {
    type FileInfo = unbox struct {
        path: Path,
        line_count: I64
    };

    empty: FileInfo;
    empty = FileInfo {
        path: "",
        line_count: 0
    };

}

impl FileInfo : ToString {
    to_string = |info| info.@path.to_string + ": " + info.@line_count.to_string;
}

count_line: Path -> IO FileInfo;
count_line = |path| (
    with_input_from_file (path, |file_handle|
        let line_count = *loop_lines(
            file_handle, 0, |count, line| continue $ (count + 1)
        );
        pure $ FileInfo { path: path, line_count: line_count }
    )
    .try(|e|
        eprintln("error: " + e);;
        pure $ FileInfo::empty
    )
);

print_file_infos: Array FileInfo -> IO ();
print_file_infos = |infos| (
    infos.to_iter.foreach_m(
        |info| println (info.to_string)
    );;
    let total_count = infos.map(@line_count).to_iter.sum;
    println ("Total: " + total_count.to_string)
);

count_lines_async: Array Path -> IO ();
count_lines_async = |files| (
    println("=== count_lines_async ===");;
    let tasks: Array (IOTask FileInfo) = *files.map_m(count_line >> AsyncIOTask::make);
    let infos: Array FileInfo = *tasks.map_m(get);
    print_file_infos(infos)
);

count_lines_sync: Array Path -> IO ();
count_lines_sync = |files| (
    println("=== count_lines_sync ===");;
    let infos: Array FileInfo = *files.map_m(count_line);
    print_file_infos(infos)
);

main: IO ();
main = (
    if false {
        let info = *count_line("a.fix");
        println(info.to_string)
    };
    do {
        let files = *find_files();
        //lift $ println(files.to_iter.map(|f| "file: "+f+"\n").join(""));;
        count_lines_async(files).lift;;
        count_lines_sync(files).lift;;
        pure ()
    }.try(|err| eprintln (err))
);
