// Semigroup trait and its several implementations (Array, Iterator, String etc).
//
// Semigroup has an associative binary operation, namely `sappend`.
// It may or may not have an identity.
//
// For details, see [Wikipedia: Semigroup](https://en.wikipedia.org/wiki/Semigroup).
module Minilib.Trait.Semigroup;

// A trait that represents a semigroup.
trait a: Semigroup {
    // `a.sappend(b)` appends `b` after `a`.
    sappend: a -> a -> a;
}

// For arrays, `sappend` appends two arrays.
impl Array a: Semigroup {
    sappend = Array::append;
}

// For iterators, `sappend` appends two iterators.
impl Iterator a: Semigroup {
    sappend = Iterator::append;
}

// For strings, `sappend` concats two strings.
impl String: Semigroup {
    sappend = String::concat;
}

// `()` does not change with `sappend`.
impl (): Semigroup {
    sappend = |b, a| ();
}

// For options, `sappand` appends two values if both is some.
impl [a: Semigroup] Option a: Semigroup {
    sappend = |b, a| (
        if a.is_none { b };
        if b.is_none { a };
        some $ a.as_some.sappend(b.as_some)
    );
}

/*
impl HashSet k v: Semigroup {
    sappend = HashSet::merge;
}
*/
