module Json;

import HashMap;
//import SimpleParser;

type Json = box union {
    nul: (),
    bool: Bool,
    number: F64,
    string: String,
    object: HashMap String Json,
    array:  Array Json
};

to_object: Array (String, Json) -> Json;
to_object = |arr| (
    let hashmap = arr.to_iter.fold(
        HashMap::empty(arr.get_size),
        |h, (k, v)| h.insert(k, v)
    );
    object $ hashmap
);


_encode_string: String -> String;
_encode_string = |str| (
    let input: Array U8 = str.get_bytes.pop_back;
    let input_size = input.get_size;
    let output: Array U8 = Array::empty(input_size * 2);
    let _DOUBLEQUOTE = 34_U8; // '"'
    let _BACKSLASH = 92_U8; // '\\'
    let output = output.push_back(_DOUBLEQUOTE);
    let output = loop (
        (0, output), |(i, output)| 
        if i >= input_size {
            break $ output
        };
        let c = input.@(i);
        let output = do {
            if (c == _BACKSLASH) {
                output.push_back(_BACKSLASH).push_back(_BACKSLASH)
            };
            if (c == _DOUBLEQUOTE) {
                output.push_back(_BACKSLASH).push_back(_DOUBLEQUOTE)
            };
            // TODO: \n, \r etc.
            output.push_back(c)
        };
        continue $ (i + 1, output)
    );
    let output = output.push_back(_DOUBLEQUOTE);
    _unsafe_from_c_str(output.push_back(0_U8))
);

_string_less_than: (String, String) -> Bool;
_string_less_than = |(str1, str2)| (
    let a1 = str1.get_bytes;
    let a2 = str2.get_bytes;
    let n1 = a1.get_size;
    let n2 = a2.get_size;
    loop(0, |i|
        if i >= n1 && i >= n2 {
            break $ false
        };
        if i >= n1 {
            break $ true
        };
        if i >= n2 {
            break $ false
        };
        let c1 = a1.@(i);
        let c2 = a2.@(i);
        if c1 < c2 {
            break $ true
        };
        if c1 > c2 {
            break $ false
        };
        continue $ i + 1
    )
);


_indent_str: I64 -> String;
_indent_str = |n| (
    Array::fill(n, " ").to_iter.concat_iter
);

_encode_object: I64 -> HashMap String Json -> String;
_encode_object = |indent, hashmap| (
    let array = hashmap.to_iter.to_array;
    let array = array.sort_by(
        |((k1,v1), (k2,v2))| _string_less_than $ (k1, k2));
    let array_size = array.get_size;
    if array_size == 0 {
        "{}"
    };
    let output = "{\n";
    let output = loop (
        (0, output), |(i, output)|
        if i >= array_size {
            break $ output
        };
        let (k, v) = array.@(i);
        let output = output + 
            _indent_str(indent + 2) + 
            k._encode_string + ": " + 
            v.encode(indent + 2) + 
            if i < array_size - 1 { ",\n" } else { "\n" };
        continue $ (i + 1, output)
    );
    let output = output +
            _indent_str(indent) + "}";
    output
);

_encode_array: I64 -> Array Json -> String;
_encode_array = |indent, array| (
    let array_size = array.get_size;
    if array_size == 0 {
        "[]"
    };
    let indent_str = _indent_str(indent);
    let output = "[\n";
    let output = loop (
        (0, output), |(i, output)|
        if i >= array_size {
            break $ output
        };
        let output = output + 
            _indent_str(indent + 2) + 
            array.@(i).encode(indent + 2) + 
            if i < array_size - 1 { ",\n" } else { "\n" };
        continue $ (i + 1, output)
    );
    let output = output +
            _indent_str(indent) + "]";
    output
);

encode: I64 -> Json -> String;
encode = |indent, json| (
    if json.is_nul {
        "null"
    };
    if json.is_bool {
        if json.as_bool { "true" } else { "false" }
    };
    if json.is_number {
        json.as_number.to_string
    };
    if json.is_string {
        json.as_string._encode_string
    };
    if json.is_object {
        json.as_object._encode_object(indent)
    };
    if json.is_array {
        json.as_array._encode_array(indent)
    };
    "" // abort?
);


decode: String -> Result ErrMsg Json;
decode = |str| err $ "";
