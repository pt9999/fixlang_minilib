// Extension of `Random` module. For example, generating a random integer in range, or a random array.
module Minilib.Math.RandomEx;

import Random;

// `random.generate_I64_range(begin, end)` generates a random integer `r`
// such that `begin <= r && r < end`.
generate_I64_range: I64 -> I64 -> Random -> (I64, Random);
generate_I64_range = |begin, end, random| (
    let (begin, end) = if begin > end { (end, begin) } else { (begin, end) };
    let n = (end - begin).to_U64;
    let (r, random) = random.generate_U64;
    let r = r / (U64::maximum / n);
    if r >= n {
        generate_I64_range(begin, end, random)  // try again
    };
    let r = r.to_I64 + begin;
    (r, random)
);

// `random.generate_array(size, f)` generates a random array.
// Each element of the array is generated by `f`.
generate_array: I64 -> (Random -> (a, Random)) -> Random -> (Array a, Random);
generate_array = |size, f, random| (
    let array = Array::empty(size);
    let inner = fix $ |inner, array, i, random| (
        if i >= size {
            (array, random)
        };
        let (r, random) = f(random);
        inner(array.push_back(r), i + 1, random)
    );
    inner(array, 0, random)
);

// Shuffles an array.
shuffle: Array a -> Random -> (Array a, Random);
shuffle = |array, random| (
    loop(
        (array, random, array.get_size - 1), |(array, random, i)|
        if i <= 0 {
            break $ (array, random)
        };
        let (j, random) = random.generate_U64;
        let j = (j % (i + 1).to_U64).to_I64;
        let tmp = array.@(i);
        let array = array.set(i, array.@(j));
        let array = array.set(j, tmp);
        continue $ (array, random, i - 1)
    )
);