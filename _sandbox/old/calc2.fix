module Main;

import Character;

import Minilib.Common.IOEx;

type Value = I64;

// box を書かないと stack overflow になる
type Expr = box union {
    ex_value: Value,
    ex_add: (Expr, Expr),
    ex_sub: (Expr, Expr),
    ex_mul: (Expr, Expr),
    ex_div: (Expr, Expr)
};

read_while: IOHandle -> (U8 -> Bool) -> IOFail (Array U8);
read_while = |input, filter| (
    let arr = Array::empty(100);
    loop_m(arr, |arr| (
        let c = *get_char(input);
        if (c == 0_U8 || not (filter(c))) {
            unget_char(input, c);;
            break_m $ arr
        };
        let arr = arr.push_back (c);
        continue_m $ arr
    ))
);

skip_whitespaces: IOHandle -> IOFail ();
skip_whitespaces = |input| (
    read_while(input, |c| is_space(c) && c != '\n');;
    pure()
);

_unsafe_to_string: Array U8 -> String;
_unsafe_to_string = |a| (
    String::_unsafe_from_c_str (a.push_back(0_U8))
);


parse_value: IOHandle -> IOFail Expr;
parse_value = |input| (
    skip_whitespaces (input);;
    let arr = *read_while(input, is_digit);
    let str = arr._unsafe_to_string;
    let res = from_string(str);
    if res.is_err {
        throw (res.as_err.to_string + ": `" + str + "`")
    };
    let value : I64 = res.as_ok;
    pure(ex_value(value))
);

parse_mul_div: IOHandle -> IOFail Expr;
parse_mul_div = |input| (
    let expr = *parse_value(input);
    skip_whitespaces(input);;
    let op = *get_char(input);
    if (op != '*' && op != '/') {
        unget_char(input, op);;
        pure(expr)
    };
    // これでは右結合になってしまう。。。
    let expr2 = *parse_mul_div(input);
    if (op == '*') {
        pure(ex_mul((expr, expr2)))
    } else {
        pure(ex_div((expr, expr2)))
    }
);

parse_add_sub: IOHandle -> IOFail Expr;
parse_add_sub = |input| (
    let expr = *parse_mul_div (input);
    skip_whitespaces(input);;
    let op = *get_char(input);
    if (op != '+' && op != '-') {
        unget_char(input, op);;
        pure(expr)
    };
    // これでは右結合になってしまう。。。
    let expr2 = *parse_add_sub(input);
    if (op == '+') {
        pure(ex_add((expr, expr2)))
    } else {
        pure(ex_sub((expr, expr2)))
  }
);

evaluate: Expr -> Value;
evaluate = |expr| (
    if expr.is_ex_add {
        let (a, b) = expr.as_ex_add;
        evaluate(a) + evaluate(b)
    };
    if expr.is_ex_sub {
        let (a, b) = expr.as_ex_sub;
        evaluate(a) - evaluate(b)
    };
    if expr.is_ex_mul {
        let (a, b) = expr.as_ex_mul;
        evaluate(a) * evaluate(b)
    };
    if expr.is_ex_div {
        let (a, b) = expr.as_ex_div;
        evaluate(a) / evaluate(b)
    };
    expr.as_ex_value
);

//  Read next character from the input. Returns 0_U8 if EOF.
get_char : IOHandle -> IOFail U8;
get_char = |input| (
    let file_ptr = *input.get_file_ptr.lift;
    let res = *FFI_CALL_IO[I32 fgetc(Ptr), file_ptr].lift;
    if res < 0_I32 {    // EOF
        pure(0_U8)
    };
    pure(res.to_U8)
);

//  Unread a character back to the input. when c == 0_U8, it does nothing.
unget_char : IOHandle -> U8 -> IOFail ();
unget_char = |input, c| (
    if (c == 0_U8) {
        pure()
    };
    let file_ptr = *input.get_file_ptr.lift;
    let res = *FFI_CALL_IO[I32 ungetc(I32, Ptr), c.to_I32, file_ptr].lift;
    if res < 0_I32 {    // EOF
        throw $ "ungetc failed"
    };
    pure()
);

//  Peek next character from the input, or 0_U8 if EOF.
peek_char : IOHandle -> IOFail U8;
peek_char = |input| (
    let c = *get_char(input);
    unget_char(input, c);;
    pure(c)
);

process_line: IOHandle -> IOHandle -> IOFail Bool;
process_line = |input, output| (
    write_string(output, ">> ");;
    fflush(output);;
    let c = *peek_char(input);
    if (c == 0_U8) {
        pure(true)  // EOF
    };
    let expr = *parse_add_sub (input);
    let c = *get_char(input);
    if (c != '\n') {
        throw $ "extra text"
    };

    let value = evaluate(expr);
    write_string (output, value.to_string + "\n");;
    pure(false)
);

drop_until_newline_or_eof: IOHandle -> IO ();
drop_until_newline_or_eof = |input| (
    do {
        read_while(input, |c| c != 0_U8 && c != '\n');;
        get_char(input);;
        pure()
    }.try(|err| pure())
);

main : IO ();
main = (
    println("Calc ver 0.11\n");;
    loop_m ((), |_|
        do {
            let eof = *process_line(IO::stdin, IO::stdout);
            if (eof) {
                break_m $ ()
            };
            continue_m $ ()
        }.try(|errmsg|
            eprintln(errmsg);;
            drop_until_newline_or_eof(IO::stdin);;
            continue_m $ ()
        )
    )
);
