module Main;


namespace List {
    type List a = box union {
        nil: (),
        cell: (List a, a)
    };

    empty: List a;
    empty = nil();

    is_empty: List a -> Bool;
    is_empty = is_nil;

    map_or: b -> ((List a, a) -> b) -> List a -> b;
    map_or = |y, f, xs| (
        if xs.is_empty {
            y
        };
        f(xs.as_cell)
    );

    advance: List a -> Option (List a, a);
    advance = |xs| (
        xs.map_or(
            none(), some
        )
    );

    push_front: a -> List a -> List a;
    push_front = |x, xs| (
        cell $ (xs, x)
    );

    cons: a -> List a -> List a;
    cons = push_front;

    fold: b -> (a -> b -> b) -> List a -> b;
    fold = |y, f, xs| (
        if xs.is_empty {
            y
        };
        let (xs, x) = xs.as_cell;
        xs.fold(f(x, y), f)     // tail recursion
    );

    reverse: List a -> List a;
    reverse = |xs| (
        xs.fold(
            List::empty, cons
        )
    );

    // `xs.append(ys)` puts `ys` after `xs`.
    append: List a -> List a -> List a;
    append = |ys, xs| (
        xs.reverse.fold(
            ys, cons
        )
    );

    from_iter: [iter: Iterator, Item iter = a] iter -> List a;
    from_iter = |iter| (
        // not to stack overflow
        iter.fold(
            List::empty, cons
        ).reverse
    );

    to_iter: List a -> StateIterator (List a) a;
    to_iter = |xs| (
        Iterator::generate(xs, List::advance)
    );
}

impl [a: ToString] List a: ToString {
    to_string = |xs| (
        let iter = xs.to_iter.map(to_string);
        let iter = iter.append((pure $ "nil"): DynIterator String);
        iter.join(":")
    );
}

impl List a: Add {
    add = |xs, ys| xs.append(ys);
}

impl List: Functor {
    map = |f, xs| (
        // not to stack overflow
        xs.reverse.fold(
            empty, |x, ys|
            cons(f(x), ys)
        )
    );
}

impl List: Monad {
    pure = |x| cons(x, empty);
    bind = |f, xs| (
        xs.reverse.fold(
            empty, |x, ys|
            f(x).append(ys)
        )
    );
}

test_append: IO ();
test_append = (
    let xs: List I64 = cons(4, cons(3, nil()));
    let ys: List I64 = cons(2, cons(1, nil()));
    let zs = xs + ys;
    println("xs + ys = " + (xs + ys).to_string);;
    println("xs.append(ys) = " + (xs.append(ys)).to_string);;
    pure()
);

test_reverse_map: IO ();
test_reverse_map = (
    let xs: List I64 = Iterator::range(0,5).from_iter;
    println(xs.to_string);;
    let xs = xs.reverse;
    println(xs.to_string);;
    let xs = xs.map(|x| x * 2);
    println(xs.to_string);;
    pure()
);

test_map_perf: IO ();
test_map_perf = (
    let iter = Iterator::range(0,100000);
    let (sum, time) = consumed_time_while_lazy(|_|
        let iter = iter.map(|x| x * 2);
        iter.fold(0, |sum, x| sum + x)
    );
    println("iter: sum = " + sum.to_string + " time="+time.to_string);;
    let (xs, time) = consumed_time_while_lazy(|_|
        List::from_iter(iter)
    );
    println("to_iter: time="+time.to_string);;
    let (sum, time) = consumed_time_while_lazy(|_|
        let xs = xs.map(|x| x * 2);
        xs.fold(0, |sum, x| sum + x)
    );
    println("list: sum = " + sum.to_string + " time="+time.to_string);;
    pure()
);

test_bind: IO ();
test_bind = (
    let xs: List I64 = Iterator::range(1,4).from_iter;
    let zs = do {
        let x = *xs;
        let y = *xs;
        pure $ "a" + (x * y).to_string
        //cons(2*x, cons(2*x+1, empty))
    };
    println(zs.to_string)
);

main: IO ();
main = (
    test_append;;
    test_reverse_map;;
    test_map_perf;;
    test_bind;;
    pure()
);
