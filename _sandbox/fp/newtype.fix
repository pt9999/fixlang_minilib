module Main;

type Newtype t a = unbox struct {
    data: a
};

namespace Newtype {
    wrap: a -> Newtype t a;
    wrap = |a| Newtype { data: a };

    unwrap: Newtype t a -> a;
    unwrap = @data;
}

// Error: circular alias
//type UserId = Newtype UserId String;
//type ProductId = Newtype ProductId String;

type UserIdType = unbox struct {};
type UserId = Newtype UserIdType String;
type ProductIdType = unbox struct {};
type ProductId = Newtype ProductIdType String;

test1: IO ();
test1 = (
    println("=== test1 ===");;
    let user_id: UserId = wrap("user1");
    let product_id: ProductId = wrap("product1");
    //let product_id: ProductId = user_id; // error
    //let product_id: ProductId = "product1"; // error
    println("user=" + user_id.unwrap + " product=" + product_id.unwrap);;
    pure()
);


// 単一バラエティの union でラップする案

type User = unbox union {
    user: String,
};

type Product = unbox union {
    product: String,
};

test2: IO ();
test2 = (
    println("=== test2 ===");;
    let user1: User = user("user1");
    let product1: Product = product("product1");
    //let product1: Product = user1;  // error
    //let product1: Product = "product1";  // error
    println("user=" + user1.as_user + " product=" + product1.as_product);;
    pure()
);

main: IO () = (
    test1;; test2;; pure()
);
