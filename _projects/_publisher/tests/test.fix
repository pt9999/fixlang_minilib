module Test;

import Minilib.Testing.UnitTest;

import Publisher.Verup;
import Publisher.ProjectFile;
import Publisher.Tests.PublishTest;

test_increment_prerelease: TestCase;
test_increment_prerelease = (
    make_table_test("test_increment_prerelease", 
        [
            ("", ok $ "2"),
            ("-", ok $ "-2"),
            ("-alpha", ok $ "-alpha2"),
            ("-alpha2", ok $ "-alpha3"),
            ("-beta", ok $ "-beta2"),
            ("-beta.100", ok $ "-beta.101"),
        ],
        |(prerelease, expected)|
        let actual = increment_prerelease(prerelease);
        assert_equal("eq", expected, actual)
    )
);

test_increment_version: TestCase;
test_increment_version = (
    make_table_test("test_increment_version", 
        [
            ("", err $ "invalid version format: "),
            ("1.0", err $ "invalid version format: 1.0"),
            ("0.0.0", ok $ "0.0.1"),
            ("1.0.0", ok $ "1.0.1"),
            ("123.456.789", ok $ "123.456.790"),
            ("1.0.0-alpha", ok $ "1.0.0-alpha2"),
            ("1.0.0-alpha5", ok $ "1.0.0-alpha6"),
            ("1.0.0-alpha9+patch1", ok $ "1.0.0-alpha10+patch1"),
            ("1.0.0-alpha9+", err $ "invalid version format: 1.0.0-alpha9+"),
            ("1.0.0-+", err $ "invalid version format: 1.0.0-+"),
        ],
        |(version, expected)|
        let actual = increment_version(version);
        assert_equal("eq", expected, actual)
    )
);

test_project_file: TestCase;
test_project_file = (
    make_test("test_project_file") $ |_|
    let project_file = ProjectFile::make("path", [
        "# a comment",
        "[[dependencies]]", "name = \"dep\"", "version = \"*\"",
        "# another comment",
        "[general]", "name = \"test\"", "version = \"1.0.0\"",
    ]);
    let actual: Result ErrMsg String = project_file.get_project_name;
    assert_equal("get_project_name", ok $ "test", actual);;
    let actual: Result ErrMsg String = project_file.get_project_version;
    assert_equal("get_project_version", ok $ "1.0.0", actual);;
    let project_file = *project_file.set_project_version("1.0.1");
    let actual: Result ErrMsg String = project_file.get_project_version;
    assert_equal("get_project_version", ok $ "1.0.1", actual);;
    pure()
);

test: IO ();
test = (
    [
        test_increment_prerelease,
        test_increment_version,
        test_project_file,
        PublishTest::testsuite,
    ]
    .run_test_driver
);