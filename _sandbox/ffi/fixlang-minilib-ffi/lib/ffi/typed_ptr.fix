module Minilib.FFI.TypedPtr;

import Minilib.Common.Common;
import Minilib.FFI.TypeName;
import Minilib.FFI.RetainedPtr;

type TypedPtr = unbox struct {
    typename: String,
    retained_ptr: RetainedPtr,   // retained ptr of `Box a`
};

to_typed_ptr: [a: GetTypeName] a -> IO TypedPtr;
to_typed_ptr = |a| (
    pure $ TypedPtr {
        typename: (typeinfo: TypeInfo a).get_typename,
        retained_ptr: *RetainedPtr::make(a),
    }
);

get: [a: GetTypeName] TypedPtr -> IO a;
get = |typed_ptr| (
    let typename = (typeinfo: TypeInfo a).get_typename;
    if typename != typed_ptr.@typename {
        undefined("from_typed_ptr: type mismatch: typed_ptr:" + typed_ptr.@typename + " target:" + typename)
    };
    typed_ptr.@retained_ptr.get
);
