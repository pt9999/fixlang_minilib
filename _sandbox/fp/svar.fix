module Main;

import Minilib.Monad.State;
import Minilib.Monad.IO;
import Minilib.Trait.Traversable;

type [m: *->*] SStore s m a = (a -> Result a a) -> Result a s;

// State variable
type [m: *->*] SVar s m a = unbox struct {
    data: s -> SStore s m a
};

namespace SVar {
    make: [m: MonadState, StateType m = s] (s -> SStore s m a) -> SVar s m a;
    make = |data| SVar { data: data };

    get: [m: MonadState, StateType m = s] SVar s m a -> m a;
    get = |svar| (
        let s = *get_state;
        let store = (svar.@data)(s);
        // uses `err` as a const functor
        let sfa = store(|a| err(a));
        pure $ sfa.as_err
    );

    set: [m: MonadState, StateType m = s] a -> SVar s m a -> m ();
    set = |a, svar| (
        mod_state_(|s|
            let store = (svar.@data)(s);
            // uses `ok` as an identity functor
            let sfs = store(|a0| ok(a));
            sfs.as_ok
        )
    );

    sset: [m: MonadState, StateType m = s] a -> SVar s m a -> m ();
    sset = SVar::set;

    mod: [m: MonadState, StateType m = s] (a -> a) -> SVar s m a -> m ();
    mod = |f, svar| (
        mod_state_(|s|
            let store = (svar.@data)(s);
            // uses `ok` as an identity functor
            let sfs = store(|a| ok(f(a)));
            sfs.as_ok
        )
    );
}


//--------------------------
type Z = unbox struct {
    d: Array I64,
    q: Array I64,
};

type SIO = StateT Z IO;

test1: IO ();
test1 = (
    let inf = 1e18;
    let n = 30;
    let z = Z {
        d: Array::fill(n, inf),
        q: []
    };
    z.eval_state_t $ do {
        //let d: Array (SVar Z SIO I64) = Array::from_map(n, |i| SVar::make(|z: Z| z[^d][i]));
        //let q: SVar Z SIO (Array I64) = SVar::make(|z: Z| z[^q]);
        let d = Array::from_map(n, |i| SVar::make(|z| z[^d][i]));
        let q = SVar::make(|z| z[^q]);
        d.@(0).sset(0);;
        q.mod(push_back(0));;
        loop_m(
            (), |_|
            let opt = (*q.get).get_last;
            if opt.is_none { break_m $ () };
            let v = opt.as_some;
            q.mod(pop_back);;
            let u = v + 1;
            when(u < n) $ do {
                let dv = *d.@(v).get;
                d.@(u).mod(min(dv + 1));;
                q.mod(push_back(u))
            };;
            continue_m $ ()
        );;
        println((*d.map_m(get)).to_string).lift_io;;
        pure()
    }
);

main: IO () = test1;
