// `Yield e a` is a generator that produces a sequence of type `e`,
// and terminates with the final result of type `a`.
module Minilib.Monad.Yield;

// A trait for the interface of generic generator monads.
trait MonadYield = Functor + Monad + MonadYieldIF;

// A trait for generic generator monads that supports `yield` operation.
trait [m: * -> *] m: MonadYieldIF {
    // The type of an element.
    type YieldType m;
    // `yield(e)` writes an element to the generator output.
    yield: YieldType m -> m ();
}

// `Yield e a` is a generator that produces a sequence of type `e`,
// terminating with the final result of type `a`.
type Yield e a = unbox union {
    y_pure: a,
    y_yield: (e, () -> Yield e a),
};

// `Yield e` is a monad.
// - `pure` produces a monadic value, which may be the final result.
// - `bind` combines two generator outputs.
impl Yield e: Monad {
    pure = y_pure;
    bind = |f, ma| (
        match ma {
            y_pure(a) => f(a),
            y_yield((e, next)) => y_yield((e, next >> bind(f))),
        }
    );
}

// `Yield e` is a functor.
//
// Note that `Yield e` supports both `Functor::map` and `Iterator::map`,
// but their functionality is completely different.
// - `Functor::map(f: a -> b)` transforms `Yield e a` to `Yield e b`.
// - `Iterator::map(f: e -> e2)` transforms `Yield e a` to an iterator of type `e2`.
impl Yield e: Functor {
    map = |f| bind(f >> pure);
}

// `Yield e` is a generator of type `e`.
impl Yield e: MonadYieldIF {
    type YieldType (Yield e) = e;
    yield = |e| y_yield $ (e, |_| pure());
}

// `Yield e a` is an Iterator of type `e`.
// Note that iterators do not return the final state, so `a` will be discarded.
impl Yield e a: Iterator {
    type Item (Yield e a) = e;
    advance = |ma| (
        match ma {
            y_pure(a) => none(),     // `a` is discarded
            y_yield((e, next)) => some $ (next(), e),
        }
    );
}

// Returns at most `n` yielded elements and the remaining state.
take_array: I64 -> Yield e a -> (Array e, Yield e a);
take_array = |n, ma| (
    loop(
        ([], ma), |(arr,ma)|
        if arr.get_size >= n { break $ (arr, ma) };
        match ma {
            y_pure(a) => break $ (arr, y_pure(a)),
            y_yield((e, next)) => continue $ (arr.push_back(e), next()),
        }
    )
);

// `fold_yield(s, f)` folds all elements to a state `s` using `f`. 
// Returns the folded state and the final result.
fold_yield: s -> (e -> s -> s) -> Yield e a -> (s, a);
fold_yield = |s, f, ma| (
    match ma {
        y_pure(a) => (s, a),
        y_yield((e, next)) => next().fold_yield(f(e, s), f),
    }
);

