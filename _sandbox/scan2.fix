module Main;

// Create an array that is the inclusive scan of an array.
//
// Conceptually, `[a0, a1, a2, ...].scan(s, op) = [s1, s2, ...]` such that
// `s0 = s, s1 = op(a0,s0), s2 = op(a1,s1), ...`.
//
// # Parameters
//
// * `s` - The initial state.
// * `op` - The function to be called on the pair of an element and the current state.
// * `arr` - The array to be scaned.
scan : s -> (a -> s -> s) -> Array a -> Array s;
scan = |s, op, arr| (
    let n = arr.get_size;
    range(0, n).fold(
        Array::empty(n), |i, result|
        let s = if i == 0 { s } else { result.@(i-1) };
        result.push_back(op(arr.@(i), s))
    )
);

// Create an array that is the exclusive scan of an array.
//
// Conceptually, `[a0, a1, a2, ...].ex_scan(s, op) = [s0, s1, s2, ...]` such that
// `s0 = s, s1 = op(a0,s0), s2 = op(a1,s1), ...`.
//
// # Parameters
//
// * `s` - The initial state.
// * `op` - The function to be called on the pair of an element and the current state.
// * `arr` - The array to be scaned.
ex_scan : s -> (a -> s -> s) -> Array a -> Array s;
ex_scan = |s, op, arr| (
    let n = arr.get_size;
    range(0, n).fold(
        Array::empty(n + 1).push_back(s), |i, result|
        result.push_back(op(arr.@(i), result.@(i)))
    )
);

main: IO ();
main = (
    let a = range(0,10).map(add(1)).to_array;
    let b = a.scan(0, add);
    println(b.to_string);;
    let b = a.ex_scan(0, add);
    println(b.to_string)
);
