// Reader monad. (a.k.a Traced, CoWriter comonad)
//
// For details, see [blog post: The Reader and Writer Monads and Comonads](https://www.olivierverdier.com/posts/2014/12/31/reader-writer-monad-comonad/).
module Minilib.Monad.Reader;

import Minilib.Trait.Monoid;
import Minilib.Trait.Semigroup;
import Minilib.Trait.Comonad;

// Reader monad wraps a function from an environment to a value.
// `e` is a type of an environment.
// `a` is a type of a value.
type Reader e a = unbox struct {
    data: e -> a
};

namespace Reader {
    // Creates a reader monad from a function.
    reader: (e -> a) -> Reader e a;
    reader = |f| Reader { data: f };

    // Runs a reader monad with the supplied environment.
    run_reader: e -> Reader e a -> a;
    run_reader = |e, ma| (ma.@data)(e);

    // A reader monad that returns the environment as a value.
    read: Reader e e;
    read = reader(|e| e);
}

// Traced comonad. This is same as a reader monad.
type Traced e a = Reader e a;

namespace Traced {
    // Creates a traced comonad from a function.
    traced: (e -> a) -> Traced e a;
    traced = Reader::reader;

    // Runs a traced commonad with the supplied environment.
    run_traced: e -> Traced e a -> a;
    run_traced = Reader::run_reader;
}

impl Reader e: Functor {
    map = |f, ma| reader(ma.@data >> f);
}

impl Reader e: Monad {
    pure = |a| reader(|_| a);
    bind = |f, ma| reader(|e| f(ma.run_reader(e)).run_reader(e));
}

impl [e: Monoid] Reader e: Comonad {
    extract = |wa| wa.run_traced(mempty);
    extend = |f, w| traced $ |c| (
        f (traced $ |c2| w.run_traced(c.sappend(c2)))
    );
}
