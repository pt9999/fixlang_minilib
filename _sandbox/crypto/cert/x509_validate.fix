module Main;

import Debug;
import HashMap;

import Minilib.Crypto.Cert.Asn1;
import Minilib.Crypto.Cert.Asn1.Der;
import Minilib.Crypto.Cert.X509;
import Minilib.Crypto.Cert.Pem;
import Minilib.Crypto.Cert.OID;
import Minilib.Crypto.Cert.Pkcs1;
import Minilib.Encoding.Binary;
import Minilib.Monad.State;
import Minilib.Monad.Error;
import Minilib.Monad.Trans;
import Minilib.Monad.FunctorM;
import Minilib.Text.StringEx;
import Minilib.Text.Hex;
import Minilib.Thread.Time;
import Minilib.Testing.UnitTest;

validate_signature: SubjectPublicKeyInfo -> Certificate -> Result ErrMsg ();
validate_signature = |public_key, certificate| (
    //eval debug_eprintln("validate_signature");
    let hashspec = _find_pkcs1_v1_5_hashspec(certificate);
    //eval debug_eprintln("hashspec=" + hashspec.is_some.to_string);
    if hashspec.is_some {
        validate_signature_pkcs1_v1_5(hashspec.as_some, public_key, certificate)
    };
    err $ "Not supported signature algorithm: " + certificate.@signature_algorithm.to_string
);

_find_pkcs1_v1_5_hashspec: Certificate -> Option Pkcs1::HashSpec;
_find_pkcs1_v1_5_hashspec = |certificate| (
    let signature_algorithm: AlgorithmIdentifier = certificate.@signature_algorithm;
    if signature_algorithm.match_name("sha256WithRSAEncryption") {
        some $ Pkcs1::hashspec_sha256
    };
    if signature_algorithm.match_name("sha-1WithRSAEncryption") {
        some $ Pkcs1::hashspec_sha1
    };
    none()
);

validate_signature_pkcs1_v1_5: Pkcs1::HashSpec -> SubjectPublicKeyInfo -> Certificate -> Result ErrMsg ();
validate_signature_pkcs1_v1_5 = |hashspec, public_key, certificate| (
    let pubkey_algorithm: AlgorithmIdentifier = public_key.@algorithm;
    if !pubkey_algorithm.match_name("rsaEncryption") {
        err $ "Public_key algorithm does not match `rsaEncryption`: " + pubkey_algorithm.to_string
    };
    let rsa_pubkey: RSAPublicKey = *decode_der_from_bytes(public_key.@subject_public_key.@data);
    let n = rsa_pubkey.@modulus;
    let e = rsa_pubkey.@public_exponent;
    let message = certificate.@tbs_certificate_bytes;
    let signature = certificate.@signature_value.@data;
    Pkcs1::rsassa_pkcs1_v1_5_verify((n, e), message, signature, hashspec)
);

read_certificates_pem: Path -> IOFail (Array Certificate);
read_certificates_pem = |path| (
    let contents = *read_file_string(path);
    let entries = *parse_pem_entries(contents).from_result;
    let certificates = *entries.map_m(|entry|
        from_bytes(entry.@bytes)
    ).from_result;
    pure $ certificates
);

test1: IOFail ();
test1 = (
    let path = Path::parse("/etc/ssl/certs/DigiCert_Global_Root_CA.pem").as_some;   // sha-1WithRSAEncryption
    //let path = Path::parse("/etc/ssl/certs/DigiCert_Global_Root_G2.pem").as_some;   // sha256WithRSAEncryption
    let certificates = *read_certificates_pem(path);
    if certificates.get_size != 1 { error $ "read_certificates_pem failed" };
    let certificate = certificates.@(0);
    let public_key = certificate.@tbs_certificate.@subject_public_key_info;
    eval *certificate.validate_signature(public_key).from_result;
    println("validation ok").lift
);

main: IO ();
main = do {
    test1
}.try(eprintln);
