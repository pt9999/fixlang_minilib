module Filer.FileItem;

import Minilib.IO.FileSystem;
import Minilib.IO.Path;
import Minilib.Terminal.Ncurses;
import Minilib.Trait.Traversable;

import Filer.Common;

type FileItem = box struct {
    file_name: String,
    file_path: String,
    stat: FileStat,
    size: I64,
    active: I64,
};

namespace FileItem {
    get_file_item: AppEnv -> String -> String -> IOFail FileItem;
    get_file_item = |app_env, dir_path, file_name| (
        let file_path = join_paths([dir_path, file_name]);
        let stat = *lstat(file_path);
        let (size, active) = *if stat.is_dir {
            (app_env.@disk_usage.@get_dir_size)(file_path)
        } else {
            pure $ (stat.st_size, 0)
        };
        pure $ FileItem {
            file_name: file_name,
            file_path: file_path,
            stat: stat,
            size: size,
            active: active,
        }
    );

    get_file_items: AppEnv -> String -> IOFail (Array FileItem);
    get_file_items = |app_env, dir_path| (
        let file_names = *list_dir(dir_path);
        file_names.map_m(|name| get_file_item(app_env, dir_path, name))
    );

    format_file_item: FileItem -> String;
    format_file_item = |item| (
        let name = item.@file_name;
        let stat = item.@stat;
        let name = if stat.is_dir { name + "/" }
        else if stat.is_symbolic_link { name }      // TODO: add readlink?
        else if stat.st_mode.bit_and(0o100_U32) == 0o100_U32 { name + "*" }
        else { name };
        let size = item.@size.format_file_size;
        let size = if item.@active != 0 { item.@active.to_string + ":" + size } else { size };
        name.xpad_right(20, ' ') +
        size.xpad_left(13, ' ')
    );

    // Convert a file size to a String in human readable format.
    // Example: 123 => "123  B", 1234 => "1.2 KB", 123456 => "123 KB", 1234567 => "1.2 MB"
    format_file_size: I64 -> String;
    format_file_size = |size| (
        let short = |x:F64| (
            if x < 10.0 { x.to_string_precision(1_U8) }
            else { x.to_string_precision(0_U8) }
        );
        if size < 1e3 { size.to_string + "  B" };
        if size < 1e6 { (size.to_F64 / 1.0e3).short + " KB" };
        if size < 1e9 { (size.to_F64 / 1.0e6).short + " MB" };
        (size.to_F64 / 1.0e9).short + " GB"
    );

    get_attr: FileItem -> ChType;
    get_attr = |item| (
        let stat = item.@stat;
        if stat.is_dir { color_pair(cp_blue).bit_or(bold) };
        if stat.is_symbolic_link { color_pair(cp_cyan).bit_or(bold) };
        if stat.st_mode.bit_and(0o100_U32) == 0o100_U32 { color_pair(cp_green).bit_or(bold) };
        color_pair(cp_white)
    );

    to_list_item: FileItem -> ListItem;
    to_list_item = |file_item| (
        ListItem {
            text: file_item.format_file_item,
            attr: file_item.get_attr
        }
    );
}
