module Main;

import Minilib.IO.FileSystem;
import Minilib.Monad.State;
import Minilib.Terminal.Ncurses;

import Filer.Common;
import Filer.FileItem;
import Filer.FileListView;
import Filer.FileView;
import Filer.ListView;
import Filer.StatusView;

type App = unbox struct {
    window: Window,
    file_list_view: FileListView,
    file_view: FileView,
    status_view: StatusView,
};

namespace App {
    make: Window -> IOFail App;
    make = |app_win| (
        let (app_w, app_h) = *app_win.get_window_size;
        let flv_w = app_w * 1 / 3;
        let flv_win = *app_win.make_sub_window(0, 0, flv_w, app_h - 1);
        let fv_win = *app_win.make_sub_window(flv_w, 0, app_w - flv_w, app_h - 1);
        let status_win = *app_win.make_sub_window(0, app_h - 1, app_w, 1);
        let flv = *FileListView::make(flv_win, *realpath("."));
        let flv = flv.mod_list_view(set_id(0) >> set_has_focus(true));
        let fv = *FileView::make(fv_win);
        let fv = fv.mod_list_view(set_id(1) >> set_has_focus(false));
        let status_view = *StatusView::make(status_win);
        pure $ App {
            window: app_win,
            file_list_view: flv,
            file_view: fv,
            status_view: status_view,
        }
    );

    with_file_list_view: StateIOF FileListView a -> StateIOF App a;
    with_file_list_view = |sm| sm.lens_state_t(act_file_list_view);

    with_file_view: StateIOF FileView a -> StateIOF App a;
    with_file_view = |sm| sm.lens_state_t(act_file_view);

    with_status_view: StateIOF StatusView a -> StateIOF App a;
    with_status_view = |sm| sm.lens_state_t(act_status_view);

    show: StateIOF App ();
    show = (
        with_file_list_view(FileListView::show);;
        with_file_view(FileView::show);;
        with_status_view(StatusView::show);;
        pure()
    );

    get_focus: StateIOF App I64;
    get_focus = (
        if *with_file_list_view(has_focus) { pure $ 0 };
        if *with_file_view(has_focus) { pure $ 1 };
        pure $ -1
    );

    set_focus: I64 -> StateIOF App ();
    set_focus = |id| (
        with_file_list_view $ with_list_view $ set_focus(id == 0);;
        with_file_view $ with_list_view $ set_focus(id == 1);;
        pure()
    );

    next_focus: StateIOF App ();
    next_focus = (
        let focus = *get_focus;
        set_focus((focus + 1) % 2)
    );

    handle_key: StateIOF App Action;
    handle_key = (
        let focus = *get_focus;
        if focus == 0 { with_file_list_view $ FileListView::handle_key };
        if focus == 1 { with_file_view $ FileView::handle_key };
        pure $ no_action()
    );

    handle_action: Action -> StateIOF App ();
    handle_action = |action| (
        with_file_list_view $ FileListView::handle_action(action);;
        with_file_view $ FileView::handle_action(action);;
        match action {
            action_next_focus() => next_focus,
            action_open_file() => (
                match *with_file_list_view(selected_item) {
                    none() => pure(),
                    some(item) => (
                        if item.@stat.is_dir {
                            with_file_list_view(FileListView::open_directory(item.@file_path))
                        };
                        if item.@stat.is_file {
                            with_file_view(FileView::open_file(item.@file_path))
                        };
                        pure()
                    )
                }
            ),
            _ => pure()
        }
    );
}

main: IO ();
main = do {
    Ncurses::run $ |win| (
        init_custom_color_pairs;;
        let app = *App::make(win);
        loop_m(
            (), |_|
            App::show;;
            let action = *App::handle_key;
            if action.is_action_quit {
                break_m $ ()
            };
            App::handle_action(action);;
            continue_m $ ()
        ).eval_state_t(app)
    )
}.try(eprintln);
