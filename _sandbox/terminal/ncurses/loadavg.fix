module Main;

import Minilib.IO.FileSystem;
import Minilib.IO.Path;
import Minilib.Monad.State;
import Minilib.Monad.IO;
import Minilib.Terminal.Ncurses;
import Minilib.Trait.Traversable;
import Minilib.Text.StringEx;

type SIO s = StateT s IOFail;

type App = unbox struct {
    window: Window,
    loadavg: Array F64,
    time: I64,
    status: String,
};

namespace App {
    make: Window -> IOFail App;
    make = |win| (
        let (w, h) = *win.get_window_size;
        pure $ App {
            window: win,
            loadavg: [],
            time: 0,
            status: "",
        }
    );

    read_loadavg: IOFail F64;
    read_loadavg = (
        with_file("/proc/loadavg", "r", |handle|
            let line = *read_line(handle);
            let fields = line.split(" ").to_array;
            let loadavg1: F64 = *fields.@(0).from_string.from_result;
            let loadavg5: F64 = *fields.@(1).from_string.from_result;
            let loadavg15: F64 = *fields.@(2).from_string.from_result;
            pure $ loadavg1
        )
    );

    collect_loadavg: I64 -> SIO App ();
    collect_loadavg = |count| (
        let value = *read_loadavg.lift_iofail;
        mod_state_(|app|
            app.mod_loadavg(|values|
                let values = values.push_back(value);
                if values.get_size <= count { values };
                values.get_sub(values.get_size - count, values.get_size)
            )
            .mod_time(add(1))
        )
    );

    render: SIO App ();
    render = (
        let app = *get_state;
        let window = app.@window;
        let (w, h) = *window.get_window_size.lift_iofail;
        let max_samples = w - 10;
        let max_bar_size = h - 3;
        collect_loadavg(max_samples);;
        let loadavg = app.@loadavg;
        let time = app.@time;
        do {
            window.clear;;
            let colors = [ cp_red, cp_green, cp_blue ];
            range(0, loadavg.get_size).fold_m(
                (), |x, _|
                let xt = x + (time - loadavg.get_size);
                let color = colors.@(xt % colors.get_size);
                window.set_attr(color_pair(color));;
                let sample = loadavg.@(x);
                let scale = 1.0;
                let bar_size = (sample * scale * max_bar_size.to_F64).to_I64;
                let bar_size = bar_size.max(0).min(max_bar_size);
                range(0, bar_size).fold_m(
                    (), |y, _|
                    window.move_add_wstr(x, h - 2 - y, "█")     // U+2588	█	FULL BLOCK
                )
            );;
            window.set_attr(color_pair(cp_white));;
            let num = loadavg.get_last.as_some_or(0.0).to_string_precision(2_U8);
            window.move_add_str(5, 0, "loadavg: " + num);;
            window.move_add_str(5, h-1, app.@status);;
            window.refresh
        }.lift_iofail
    );
}


main: IO ();
main = do {
    Ncurses::run $ |win| (
        //init_custom_color_pairs;;
        let app = *App::make(win);
        let key_input_timeout = 1000; // milliseconds
        app.@window.timeout(key_input_timeout);;
        loop_m(
            (), |_|
            App::render;;
            let key = *app.@window.get_key.lift_iofail;
            if key == "q" || key == "^[" { break_m $ () };
            mod_state_(|app| app.set_status("key=" + key.to_string));;
            continue_m $ ()
        ).eval_state_t(app)
    )
}.try(eprintln);
