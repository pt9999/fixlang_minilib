module Publisher.Tests.MockFs;

import HashMap;

import Minilib.Common.IORef;
import Minilib.Monad.Error;
import Minilib.Monad.IO;

import Publisher.Common;
import Publisher.Fs;

//  モックのファイルシステム
type MockFs = unbox struct {
    //  カレントディレクトリ
    cwd: Path,
    //  ファイル (ファイルパスからファイルの内容へのマップ)
    files: HashMap Path String,
    //  ディレクトリ (ファイルパスから空文字列へのマップ)
    directories: HashMap Path String,
};

namespace MockFs {
    empty: MockFs;
    empty = MockFs {
        cwd: "/work",
        files: HashMap::empty(10),
        directories: HashMap::empty(10),
    };

    add_file: Path -> String -> MockFs -> MockFs;
    add_file = |path, contents, fs| (
        fs.mod_files(insert(path, contents))
    );

    get_file: Path -> MockFs -> Option String;
    get_file = |path, fs| (
        fs.@files.find(path)
    );

    add_directory: Path -> MockFs -> MockFs;
    add_directory = |path, fs| (
        fs.mod_directories(insert(path, ""))
    );

    to_api: [m: MonadIO] IORef MockFs -> m FileSystemApi;
    to_api = |ioref_fs| (
        let fs =  *ioref_fs.get.lift_io;
        pure $ FileSystemApi {
            file_exists: |path| (
                pure $ fs.@files.find(path).is_some
            ),
            directory_exists: |path| (
                pure $ fs.@directories.find(path).is_some
            ),
            chdir: |path| (
                if fs.@directories.find(path).is_none {
                    error $ "Directory not found: " + path
                };
                let fs =  fs.set_cwd(path); 
                ioref_fs.IORef::set(fs).lift_io
            ),
            realpath: |path| (
                pure $ path
            ),
            read_file_string: |path| (
                match fs.@files.find(path) {
                    none() => error $ "File not found: " + path,
                    some(contents) => pure $ contents
                }
            ),
            write_file_string: |path, contents| (
                let fs =  fs.add_file(path, contents);
                ioref_fs.IORef::set(fs).lift_io
            ),
        }
    );
}
