module Main;

import Minilib.Monad.IteratorT;
import Minilib.Monad.State;
import Minilib.Monad.IO;
import Minilib.Monad.Error;
import Minilib.Monad.Trans;
import Minilib.Trait.Traversable;
import Minilib.Testing.UnitTest;

test_empty: TestCase;
test_empty = (
    make_test("test_empty") $ |_|
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::empty;
    assert_equal("is_none", ok(true), xs.advance.map(is_none))
);

test_is_empty: TestCase;
test_is_empty = (
    make_test("test_is_empty") $ |_|
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::empty;
    assert_equal("is_empty", ok(true), xs.is_empty);;
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::empty.push_front(42);
    assert_equal("is_empty", ok(false), xs.is_empty);;
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::make $ err("invalid");
    assert_equal("is_empty", err("invalid"), xs.is_empty);;
    pure()
);

test_get_first: TestCase;
test_get_first = (
    make_test("test_get_first") $ |_|
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::empty;
    assert_equal("get_first", ok(none()), xs.get_first);;
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::empty.push_front(42);
    assert_equal("get_first", ok(some(42)), xs.get_first);;
    let xs: IteratorT (Result ErrMsg) I64 = IteratorT::make $ err("invalid");
    assert_equal("get_first", err("invalid"), xs.get_first);;
    pure()
);

test_push_front: TestCase;
test_push_front = (
    make_test("test_push_front") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::empty.push_front(42);
    let opt = *xs.advance;
    assert_true("is_some", opt.is_some);;
    let (x, xs2) = opt.as_some;
    assert_equal("x", 42, x);;
    assert_true("empty", *xs2.is_empty)
);

test_push_m_front: TestCase;
test_push_m_front = (
    make_test("test_push_m_front") $ |_|
    let inc: StateT I64 IOFail I64 = make_state_t_monad $ |i| pure $ (i + 1, i);
    let xs: IteratorT (StateT I64 IOFail) I64 = (
        IteratorT::empty
        .push_m_front(inc)
        .push_m_front(inc)
        .push_m_front(inc)
    );
    do {
        put_state(100);;
        let arr = *xs.to_array_m;
        assert_equal("arr", [100, 101, 102], arr).lift_iofail;;
        assert_equal("state", 103, *get_state).lift_iofail;;
        pure()
    }.eval_state_t(0)
);

test_from_m_iter: TestCase;
test_from_m_iter = (
    make_test("test_from_m_iter") $ |_|
    let inc: StateT I64 IOFail I64 = make_state_t_monad $ |i| pure $ (i + 1, i);
    let xs: Iterator (StateT I64 IOFail I64) = (
        Iterator::empty
        .push_front(inc)
        .push_front(inc)
        .push_front(inc)
    );
    let xs: IteratorT (StateT I64 IOFail) I64 = IteratorT::from_m_iter $ xs;
    do {
        put_state(0);;
        let arr = *xs.to_array_m;
        assert_equal("arr", [0, 1, 2], arr).lift_iofail;;
        assert_equal("state", 3, *get_state).lift_iofail;;
        put_state(100);;
        let arr = *xs.to_array_m;
        assert_equal("arr", [100, 101, 102], arr).lift_iofail;;
        assert_equal("state", 103, *get_state).lift_iofail;;
        pure()
    }.eval_state_t(0)
);

test_from_iter: TestCase;
test_from_iter = (
    make_test("test_from_iter") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::from_iter(Iterator::empty.push_front(1).push_front(2));
    let arr = *xs.to_array_m;
    assert_equal("arr", [2, 1], arr)
);

test_from_array: TestCase;
test_from_array = (
    make_test("test_from_array") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::from_array([1, 2, 3]);
    let arr = *xs.to_array_m;
    assert_equal("arr", [1, 2, 3], arr)
);

test_to_array_m: TestCase;
test_to_array_m = (
    make_test("test_to_array_m") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::empty.push_front(1).push_front(2).push_front(3);
    let arr = *xs.to_array_m;
    assert_equal("arr", [3, 2, 1], arr)
);

test_append: TestCase;
test_append = (
    make_test("test_append") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::from_array([1, 2, 3]);
    let ys: IteratorT IOFail I64 = IteratorT::from_array([4, 5]);
    let arr = *(xs.append(ys)).to_array_m;
    assert_equal("arr", [1, 2, 3, 4, 5], arr)
);

test_fold_m: TestCase;
test_fold_m = (
    make_test("test_fold_m") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::from_array([1, 2, 3]);
    let s = *xs.fold_m("", |s, i| pure $ s + i.to_string + ",");
    assert_equal("s", "1,2,3,", s)
);

test_functor_map: TestCase;
test_functor_map = (
    make_test("test_functor_map") $ |_|
    let xs: IteratorT IOFail I64 = IteratorT::from_array([1, 2, 3]);
    let xs: IteratorT IOFail String = xs.map(to_string);
    let arr = *xs.to_array_m;
    assert_equal("arr", ["1", "2", "3"], arr)
);

test_monad_pure: TestCase;
test_monad_pure = (
    make_test("test_monad_pure") $ |_|
    let xs: IteratorT IOFail I64 = pure(1);
    let arr = *xs.to_array_m;
    assert_equal("arr", [1], arr)
);

test_monad_bind: TestCase;
test_monad_bind = (
    make_test("test_monad_bind") $ |_|
    let xs: IteratorT IOFail (I64, I64) = do {
        let x = *IteratorT::from_array([1,2]);
        let y = *IteratorT::from_array([3,4]);
        pure $ (x, y)
    };
    let arr = *xs.to_array_m;
    assert_equal("arr", [(1,3),(1,4),(2,3),(2,4)], arr)
);

test_monad_trans_lift_t: TestCase;
test_monad_trans_lift_t = (
    make_test("test_monad_trans_lift_t") $ |_|
    let xs: IteratorT (Result ErrMsg) (I64, I64) = do {
        let x = *IteratorT::from_array([1,2]);
        let y = *ok(3).lift_t;
        pure $ (x, y)
    };
    assert_equal("arr", ok([(1,3),(2,3)]), xs.to_array_m);;
    let xs: IteratorT (Result ErrMsg) (I64, I64) = do {
        let x = *IteratorT::from_array([1,2]);
        let y = *err("error").lift_t;
        pure $ (x, y)
    };
    assert_equal("arr", err("error"), xs.to_array_m);;
    pure()
);

test_monad_error_error: TestCase;
test_monad_error_error = (
    make_test("test_monad_error_error") $ |_|
    let xs1: IteratorT (Result ErrMsg) I64 = error("error");
    assert_equal("xs1.advance.map(@0)", err("error"), xs1.advance.map(map(Tuple2::@0)));;
    assert_equal("xs1.to_array_m", err("error"), xs1.to_array_m);;
    let xs2: IteratorT (Result ErrMsg) I64 = xs1.push_front(42);
    assert_equal("xs2.advance.map(@0)", ok(some(42)), xs2.advance.map(map(Tuple2::@0)));;
    assert_equal("xs2.to_array_m", err("error"), xs2.to_array_m);;
    pure()
);

test_monad_error_catch: TestCase;
test_monad_error_catch = (
    make_test("test_monad_error_catch") $ |_|
    let xs1: IteratorT (Result ErrMsg) I64 = do {
        let x = *IteratorT::from_array([1,2,3]);
        if x == 3 { error("error") };
        pure(x)
    };
    assert_equal("xs1.to_array_m", err("error"), xs1.to_array_m);;
    let xs2: IteratorT (Result ErrMsg) I64 = xs1.catch(|errmsg|
        let x = *IteratorT::from_array([4,5,6]);
        if x == 6 { error("error2") };
        pure(x)
    );
    assert_equal("xs2.to_array_m", err("error2"), xs2.to_array_m);;
    let xs3: IteratorT (Result ErrMsg) I64 = xs2.catch(|errmsg|
        let x = *IteratorT::from_array([7,8,9]);
        pure(x)
    );
    assert_equal("xs3.to_array_m", ok([1,2,4,5,7,8,9]), xs3.to_array_m);;
    pure()
);

// TODO: implement `with_tempfile` to Minilib.IO.FileSystem
temp_filepath: Path;
temp_filepath = Path::parse(".fixlang/tmp.iterator_t_test.tmp").as_some;

test_monad_io_lift_io: TestCase;
test_monad_io_lift_io = (
    make_test("test_monad_io_lift_io") $ |_|
    with_file(temp_filepath, "w", |handle|
        let xs1: IteratorT IOFail (I64,I64) = do {
            let x = *IteratorT::from_array([1,2]);
            let res = *write_string(handle, "x="+ x.to_string + ",").to_result.lift_io;
            assert_true("write_string", res.is_ok).lift_iofail;;
            let y = *IteratorT::from_array([3,4]);
            let res = *write_string(handle, "y="+y.to_string + ",").to_result.lift_io;
            assert_true("write_string", res.is_ok).lift_iofail;;
            pure $ (x, y)
        };
        assert_equal("arr", [(1,3),(1,4),(2,3),(2,4)], *xs1.to_array_m)
    );;
    let contents = *read_file_string(temp_filepath);
    assert_equal("contents", "x=1,y=3,y=4,x=2,y=3,y=4,", contents)
);

test_monad_iofail_lift_iofail: TestCase;
test_monad_iofail_lift_iofail = (
    make_test("test_monad_iofail_lift_iofail") $ |_|
    with_file(temp_filepath, "w", |handle|
        let xs1: IteratorT IOFail I64 = do {
            let x = *IteratorT::from_array([1,2,3]);
            write_string(handle, "x="+x.to_string + ",").lift_iofail;;
            if x == 3 { throw("error").lift_iofail };
            pure(x)
        };
        let xs2: IteratorT IOFail String = xs1.map(to_string).catch(|errmsg|
            write_string(handle, "catch="+errmsg).lift_iofail;;
            pure(errmsg)
        );
        let res = *xs2.to_array_m.to_result.lift;
        assert_equal("arr", ok $ ["1", "2", "error"], res)
    );;
    let contents = *read_file_string(temp_filepath);
    assert_equal("contents", "x=1,x=2,x=3,catch=error", contents)
);

test_monad_state: TestCase;
test_monad_state = (
    make_test("test_monad_state") $ |_|
    let xs: IteratorT (StateT (Array String) IOFail) () = do {
        let n = 10;
        let i: I64 = *Iterator::range(0, n).lift_iter;
        let j = *Iterator::range(i, n).lift_iter;
        let k = *Iterator::range(j, n).lift_iter;
        mod_state(push_back((i,j,k).to_string)).lift_t
    };
    let result = *xs.to_iter_m.exec_state_t([]);
    //println("result.size="+result.get_size.to_string).lift;;
    //println("result="+result.to_string).lift;;
    assert_true("result", result.find_by(eq("(1, 2, 3)")).is_some)
);

// TODO: add tests

main: IO ();
main = (
    [
        test_empty,
        test_is_empty,
        test_get_first,
        test_push_front,
        test_push_m_front,
        test_from_m_iter,
        test_from_iter,
        test_from_array,
        test_to_array_m,
        test_append,
        test_fold_m,
        test_functor_map,
        test_monad_pure,
        test_monad_bind,
        test_monad_trans_lift_t,
        test_monad_error_error,
        test_monad_error_catch,
        test_monad_io_lift_io,
        test_monad_iofail_lift_iofail,
        test_monad_state,
    ]
    .run_test_driver
);