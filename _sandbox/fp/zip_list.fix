module Minilib.Functor.ZipList;

import Minilib.Trait.Applicative;

type ZipList a = unbox struct {
    data: DynIterator a
};

make: DynIterator a -> ZipList a;
make = |iter| ZipList { data: iter };

from_iter: DynIterator a -> ZipList a;
from_iter = make;

to_iter: ZipList a -> DynIterator a;
to_iter = @data;

from_array: Array a -> ZipList a;
from_array = Array::to_iter >> to_dyn >> ZipList::from_iter;

to_array: ZipList a -> Array a;
to_array = ZipList::to_iter >> Iterator::to_array;

impl ZipList: Functor {
    map = |f, zl| ZipList::make $ zl.@data.Iterator::map(f).to_dyn;
}

impl ZipList: Applicative {
    pure_a = |x| ZipList::make $ pure $ x;
    app_a = |fs, xs| (
        ZipList::make $
        fs.to_iter.zip(xs.to_iter).map(|(f, x)| f(x)).to_dyn
    );
}
