module Main;

import Debug;
import Random;

import SHA256;
import StringEx;
import UnitTest;

bytes_to_string_hex: Array U8 -> String;
bytes_to_string_hex = |u8array| (
    u8array.to_iter.fold(
        [], |output, u8|
        output.push_back(u8.shift_right(4_U8).bit_and(15_U8).encode_hex_char)
              .push_back(u8.shift_right(0_U8).bit_and(15_U8).encode_hex_char)
    ).push_back(0_U8)._unsafe_from_c_str.to_lower
);

make_random_bytes: I64 -> I64 -> Array U8;
make_random_bytes = |size, seed| (
    let random = Random::init_by_seed(seed.to_U64);
    let iter = Iterator::generate(random, generate_U64 >> some);
    iter.map(to_U8).take(size).to_array
);

test_digest_ok : (String, Array U8, String) -> TestCase;
test_digest_ok = |(name, bytes, expected)| (
    let testname = "test_digest_ok (" + name + "," + expected.to_string + ")";
    make_test(testname) $ |_|
    //let _ = *write_file_bytes(Path::parse("tmp." + name).as_some, bytes);
    let actual = SHA256::digest(bytes).bytes_to_string_hex;
    let _ = *(assert_equal("eq", expected, actual));
    pure()
);

test_digest: TestCase;
test_digest = (
    [
        test_digest_ok $ ("empty", [], "e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"),
        test_digest_ok $ ("abcdefg", "abcdefg".get_bytes.pop_back, "7d1a54127b222502f5b79b5fb0803061152a44f92b37e23c6527baf665d4da9a"),
        test_digest_ok $ ("0-255", Array::from_map(256, |i| i.to_U8), "40aff2e9d2d8922e47afd4648e6967497158785fbd1da870e7110266bf944880"),
        test_digest_ok $ ("random_567", make_random_bytes(100000, 567), "7cb4b51dc7885156470e71f31f93eb98e2a6b5c3a90444bb609936354179bffc"),
        TestCase::empty
    ].run_tests
);

main: IO ();
main = (
    [
        test_digest,
        TestCase::empty
    ]
    .run_test_driver
);
