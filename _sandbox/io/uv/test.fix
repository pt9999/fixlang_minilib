module Test;

import AsyncTask;
import Minilib.IO.Uv;
import Minilib.Testing.UnitTest;

test_loop_simple: TestCase;
test_loop_simple = (
    make_test("test_loop_simple") $ |_|
    let loop = *UvLoop::make;
    loop.run_default;;
    pure()
);

test_loop_default: TestCase;
test_loop_default = (
    make_test("test_loop_default") $ |_|
    let loop = UvLoop::default;
    loop.run_default;;
    pure()
);

test_fs_open: TestCase;
test_fs_open = (
    make_test("test_fs_open") $ |_|
    let var = *Var::make("").lift;
    let loop = *UvLoop::make;
    let path = "tmp.file.tmp";
    let flags = UvFsFlags::_O_CREAT.bit_or(UvFsFlags::_O_WRONLY).bit_or(UvFsFlags::_O_TRUNC);
    let mode = 0o666.to_CInt;
    let cb = |fs: UvFs| (
        let result = *fs.get_result;
        if result >= 0 {
            //println("fs open callback called");;
            var.Var::set("open success")
        } else {
            var.Var::set("open error: " + Uv::strerror(result.to_CInt))
        }
    );
    let fs = *UvFs::open(loop, path, flags, mode, cb);
    loop.run_default;;
    assert_equal("eq", "open success", *var.get.lift);;
    pure()
);

test_pipe_open: TestCase;
test_pipe_open = (
    make_test("test_pipe_open") $ |_|
    let loop = *UvLoop::make;
    let pipe = *UvPipe::init(loop);
    pipe.open(UvFile::stdout);;
    loop.run_default;;
    pure()
);

test_pipe_write: TestCase;
test_pipe_write = (
    make_test("test_pipe_write") $ |_|
    let var = *Var::make("").lift;
    let loop = *UvLoop::make;
    let pipe = *UvPipe::init(loop);
    pipe.open(UvFile::stdout);;
    let cb = |write: UvReq, status: CInt| (
        if status >= 0.to_CInt {
            println("write success");;
            var.Var::set("write success")
        } else {
            var.Var::set("write error: " + Uv::strerror(status))
        }
    );
    let buf = "test_pipe_write: hello world\n".get_bytes.pop_back;
    pipe.write(buf, cb);;
    pipe.close.lift;;
    loop.run_default;;
    pure()
);

test: IO ();
test = (
    [
        test_loop_simple,
        test_loop_default,
        test_fs_open,
        test_pipe_open,
        test_pipe_write,
    ]
    .run_test_driver
);
