module Minilib.Crypto.Cert.X509Time;

import Time;

import Minilib.Crypto.Cert.Asn1;
import Minilib.Crypto.Cert.Asn1Der;
import Minilib.Monad.Error;
import Minilib.Text.StringEx;

// Defined in order to implement ToString trait.
// Same as `Time::Time`.
type X509Time = unbox struct { sec : I64, nanosec : U32 };

namespace X509Time {
    make: I64 -> U32 -> X509Time;
    make = |sec, nanosec| X509Time {
        sec: sec,
        nanosec: nanosec
    };

    to_time: X509Time -> Time::Time;
    to_time = |asn1_time| Time::Time {
        sec: asn1_time.@sec,
        nanosec: asn1_time.@nanosec
    };

    from_time: Time::Time -> X509Time;
    from_time = |time| X509Time {
        sec: time.@sec,
        nanosec: time.@nanosec
    };

    from_utc: Time::DateTime -> Result ErrMsg X509Time;
    from_utc = Time::from_utc >> map(from_time);

    to_utc: X509Time -> Result ErrMsg Time::DateTime;
    to_utc = to_time >> Time::to_utc;
}

namespace DateTimeFormat {
    // (year, month, day, hour, minute, second, nanosec) -> String
    type FormatFunc = (String, String, String, String, String, String, String) -> String;

    format: Time::DateTime -> FormatFunc -> String;
    format = |datetime, formatter| (
        let zero_padding = |i64, prec| (
            let str = Array::fill(prec - 1, '0')._unsafe_to_string + i64.to_string;
            str.substring(str.get_size - prec, str.get_size)
        );
        let year = zero_padding(datetime.@year.to_I64, 4);
        let month = zero_padding(datetime.@month.to_I64, 2);
        let day = zero_padding(datetime.@day_in_month.to_I64, 2);
        let hour = zero_padding(datetime.@hour.to_I64, 2);
        let minute = zero_padding(datetime.@min.to_I64, 2);
        let second = zero_padding(datetime.@sec.to_I64, 2);
        let nanosec = zero_padding(datetime.@nanosec.to_I64, 9);
        formatter $ (year, month, day, hour, minute, second, nanosec)
    );
}


impl X509Time: ToString {
    to_string = |asn1_time| (
        let res = asn1_time.to_utc;
        if res.is_err { "err:" + res.as_err };
        let datetime = res.as_ok;
        DateTimeFormat::format(
            datetime,
            |(year, month, day, hour, minute, second, nanosec)|
            year + "-" + month + "-" + day + "T" + hour + ":" + minute  + ":" + second + "." + nanosec + "Z"
        )
    );
}

//type GeneralizedTime = X509Time; // YYYYMMDDHHMMSSZ for RFC5280
//type UTCTime = X509Time; // YYMMDDHHMMSSZ for RFC5280

impl X509Time: DecodeDer {
    decode_obj = (
        choice $ [
            decode_utc_time,
            decode_generalized_time
        ]
    );
}

decode_generalized_time: [m: MonadReadByte] m X509Time;
decode_generalized_time = (
    let (identifier, contents) = *decode_primitive;
    eval *assert_tag(tag_generalized_time, identifier);
    // contents = YYYYMMDDHHMMSSZ
    let str = contents._unsafe_to_string;
    if str.get_size != 15 {
        error $ "decode_generalized_time: length mismatch: " + str
    };
    if str.get_bytes.@(14) != 'Z' {
        error $ "decode_generalized_time: invalid format: " + str
    };
    do {
        let datetime = Time::DateTime {
            year: *from_string(str.substring(0, 4)).map(I64::to_I32),
            month: *from_string(str.substring(4, 6)).map(I64::to_U8),
            day_in_month: *from_string(str.substring(6, 8)).map(I64::to_U8),
            hour: *from_string(str.substring(8, 10)).map(I64::to_U8),
            min: *from_string(str.substring(10, 12)).map(I64::to_U8),
            sec: *from_string(str.substring(12, 14)).map(I64::to_U8),
            nanosec: 0_U32,
            day_in_week: 0_U8,
            day_in_year: 0_U32,
            is_dst: none()
        };
        X509Time::from_utc(datetime)
    }.from_result_t
);

decode_utc_time: [m: MonadReadByte] m X509Time;
decode_utc_time = (
    let (identifier, contents) = *decode_primitive;
    eval *assert_tag(tag_utc_time, identifier);
    // contents = YYMMDDHHMMSSZ
    let str = contents._unsafe_to_string;
    if str.get_size != 13 {
        error $ "decode_utc_time: length mismatch: " + str
    };
    if str.get_bytes.@(12) != 'Z' {
        error $ "decode_utc_time: invalid format: " + str
    };
    do {
        let datetime = Time::DateTime {
            year: *from_string(str.substring(0, 2)).map(
                        // RFC5280 4.1.2.5.1. UTCTime
                        // Where YY is greater than or equal to 50, the year SHALL be interpreted as 19YY; and
                        // Where YY is less than 50, the year SHALL be interpreted as 20YY.
                        |yy| if yy >= 50 { yy + 1900 } else { yy + 2000 }
                    ).map(I64::to_I32),
            month: *from_string(str.substring(2, 4)).map(I64::to_U8),
            day_in_month: *from_string(str.substring(4, 6)).map(I64::to_U8),
            hour: *from_string(str.substring(6, 8)).map(I64::to_U8),
            min: *from_string(str.substring(8, 10)).map(I64::to_U8),
            sec: *from_string(str.substring(10, 12)).map(I64::to_U8),
            nanosec: 0_U32,
            day_in_week: 0_U8,
            day_in_year: 0_U32,
            is_dst: none()
        };
        X509Time::from_utc(datetime)
    }.from_result_t
);

