module Main;

import Minilib.Common.TimeEx;
import Minilib.Thread.Future;
import Minilib.Thread.TaskPool;
import Minilib.Monad.Reader;
import Minilib.Monad.IO;
import Minilib.Monad.Trans;
import Minilib.Trait.Traversable;

type RIO e a = ReaderT e IOFail a;

trait e: HasTaskPool {
    get_taskpool: e -> TaskPool;
}

impl TaskPool: HasTaskPool {
    get_taskpool = |e| e;
}

type Tasklet a = Future (Result ErrMsg a);

namespace Tasklet {
    make_tasklet: [e: HasTaskPool] IOFail a -> RIO e (Tasklet a);
    make_tasklet = |iof| (
        let pool = (*ask).get_taskpool;
        Future::make(pool, iof.to_result).lift_iofail
    );

    then: [e: HasTaskPool] (a -> IOFail b) -> Tasklet a -> RIO e (Tasklet b);
    then = |f, tasklet| (
        make_tasklet(do {
            let res_a = *tasklet.get;
            match res_a {
                err(e) => throw $ e,
                ok(a) => f(a)
            }
        })
    );

    catch: [e: HasTaskPool] (ErrMsg -> IOFail a) -> Tasklet a -> RIO e (Tasklet a);
    catch = |f, tasklet| (
        make_tasklet(do {
            let res_a = *tasklet.get;
            match res_a {
                err(e) => f(e),
                ok(a) => pure $ a
            }
        })
    );

    get_all: Array (Tasklet a) -> IOFail (Array (Result ErrMsg a));
    get_all = |tasklets| (
        tasklets.to_iter.map(get).collect_m
    );
}


test1: IO ();
test1 = do {
    println("=== test1 ===").lift;;
    let rio: RIO TaskPool () = do {
        let tasklets = *range(0,8).map(|i|
            let tasklet = *make_tasklet(do {
                usleep((100 * (i+1)).to_U32);;
                if i % 2 == 1 { throw $ "fail" + i.to_string };
                pure $ i
            });
            let tasklet = *tasklet.then(|i| pure $ "success" + i.to_string);
            let tasklet = *tasklet.catch(|err| pure $ "error:" + err);
            let tasklet = *tasklet.then(|str| println(str).lift);
            pure $ tasklet
        ).collect_m;
        //usleep(10000_U32).lift_iofail // TODO: consider waiting all tasklets
        tasklets.get_all.forget.lift_iofail
    };
    let pool = *TaskPool::make(3).lift;
    rio.run_reader_t(pool)
}.try(eprintln);

main: IO () = test1;
