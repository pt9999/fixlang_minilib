// Writer Monad. (a.k.a Env, CoReader Comonad)
//
// cf. https://www.olivierverdier.com/posts/2014/12/31/reader-writer-monad-comonad/
module Minilib.Monad.Writer;

import Minilib.Trait.Monoid;
import Minilib.Trait.Semigroup;
import Minilib.Trait.Comonad;

type Writer e a = unbox struct {
    data: (e, a)
};

namespace Writer {
    writer: e -> a -> Writer e a;
    writer = |e, a| Writer { data: (e, a) };

    run_writer: Writer e a -> (e, a);
    run_writer = @data;

    write: [e: Monoid] e -> Writer e ();
    write = |e| writer(e, ());

    get_env: Writer e a -> e;
    get_env = |ma| ma.@data.@0;

    get_value: Writer e a -> a;
    get_value = |ma| ma.@data.@1;
}

type Env e a = Writer e a;

namespace Env {
    env: e -> a -> Env e a;
    env = Writer::writer;

    to_tuple: Env e a -> (e, a);
    to_tuple = @data;
}

impl Writer e: Functor {
    map = |f, ma| writer(ma.get_env, f(ma.get_value));
}

impl [e: Monoid] Writer e: Monad {
    pure = |a| writer(mempty, a);
    bind = |f, ma| (
        let mb = f(ma.get_value);
        let env = ma.get_env.sappend(mb.get_env);
        writer(env, mb.get_value)
    );
}

impl Writer e: Comonad {
    extract = get_value;
    extend = |f, wb| writer(wb.get_env, f(wb));
}