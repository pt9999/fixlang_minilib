module Main;

import Minilib.IO.FileSystem;
import Minilib.IO.Path;
import Minilib.Monad.State;
import Minilib.Monad.IO;
import Minilib.Terminal.Ncurses;
import Minilib.Trait.Traversable;
import Minilib.Text.StringEx;

type SIO s = StateT s IOFail;

type App = unbox struct {
    window: Window,
    loadavg: Array F64,
};

namespace App {
    make: Window -> IOFail App;
    make = |win| (
        let (w, h) = *win.get_window_size;
        pure $ App {
            window: win,
            loadavg: [],
        }
    );

    read_loadavg: IOFail F64;
    read_loadavg = (
        with_file("/proc/loadavg", "r", |handle|
            let line = *read_line(handle);
            let fields = line.split(" ").to_array;
            let loadavg1: F64 = *fields.@(0).from_string.from_result;
            let loadavg5: F64 = *fields.@(1).from_string.from_result;
            let loadavg15: F64 = *fields.@(2).from_string.from_result;
            pure $ loadavg1
        )
    );

    collect_loadavg: I64 -> SIO App ();
    collect_loadavg = |count| (
        let value = *read_loadavg.lift_iofail;
        mod_state_(
            mod_loadavg(|values|
                let values = values.push_back(value);
                if values.get_size <= count { values };
                values.get_sub(values.get_size - count, values.get_size)
            )
        )
    );

    render: SIO App ();
    render = (
        let app = *get_state;
        let window = app.@window;
        let (w, h) = *window.get_window_size.lift_iofail;
        collect_loadavg(h-1);;
        let loadavg = app.@loadavg;
        do {
            window.clear;;
            range(0, loadavg.get_size).fold_m(
                (), |y, _|
                let value = loadavg.@(y) / 3.0;
                let max_bar_size = w - 10;
                let bar_size = (max_bar_size.to_F64 * value).to_I64;
                let bar_size = bar_size.max(0).min(max_bar_size);
                let bar = Array::fill(bar_size, '#')._unsafe_to_string;
                let num = loadavg.@(y).to_string_precision(2_U8);
                window.move_add_str(0, y, bar + " " + num)
            );;
            window.refresh
        }.lift_iofail
    );
}


main: IO ();
main = do {
    Ncurses::run $ |win| (
        //init_custom_color_pairs;;
        let app = *App::make(win);
        let key_input_timeout = 1000; // milliseconds
        app.@window.timeout(key_input_timeout);;
        loop_m(
            (), |_|
            App::render;;
            let key = *app.@window.getch.lift_iofail;
            //if key < 0 { break_m $ () };
            continue_m $ ()
        ).eval_state_t(app)
    )
}.try(eprintln);
