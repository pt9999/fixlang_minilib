module Minilib.Crypto.Tls13.Handshake;

import AsyncTask;
import Debug;

import Minilib.Crypto.Tls13.Types;
import Minilib.Crypto.Tls13.RandGen;
import Minilib.Crypto.Tls13.ECDHE;
import Minilib.Crypto.Tls13.ECDSA;
import Minilib.Crypto.Tls13.KeyShare;
import Minilib.Crypto.Tls13.Extensions;
import Minilib.Crypto.Tls13.Connection;
import Minilib.Crypto.Tls13.Protection;
import Minilib.Crypto.Tls13.HandshakeProtocol;
import Minilib.Crypto.Tls13.RecordProtocol;
//import Minilib.Encoding.Binary;
import Minilib.Monad.State;
import Minilib.Monad.Error;
import Minilib.Text.Hex;
import Minilib.Text.StringEx;

// TODO: support other key agreement type
type KeyAgreement = ECDHE;

type ClientHelloState = unbox struct {
    client_hello_records: Array TLSPlaintext,
    key_agreement: KeyAgreement,
};

impl ClientHelloState: ToString {
    to_string = |hello| (
        "ClientHelloState {" +
        " client_hello_records=" + hello.@client_hello_records.to_string +
        " key_agreement=" + hello.@key_agreement.to_string +
        "}"
    );
}

namespace ClientHelloState {
    make: [m: MonadRandGen] String -> m ClientHelloState;
    make = |server_name| (
        let hello = ClientHello::empty;
        let hello = hello.init_random(*randgen_bytes(32));
        // init server_name
        let hello = hello.add_extension(Extension::server_name $
            ServerNameList::make([
                ServerName::make(server_name)
            ])
        );
        // init key_share
        // TODO: support other key agreement type
        let key_agreement = *ECDHE::make(ECParam::secp256r1);
        let ks: KeyShare = ks_client_hello $ KeyShareClientHello::make $ [
            key_agreement.to_key_share_entry
        ];
        let hello = hello.add_extension(Extension::key_share $ ks);
        // 9.2. Mandatory-to-Implement Extensions
        // TODO: cookie
        // TODO: signature_algorithms_cert

        eval debug_println("client_hello: extensions=" + hello.@extensions.to_string);
        let handshake: Handshake = Handshake::client_hello $ hello;
        let handshake_bytes: Array U8 = marshal_to_bytes(handshake);
        let client_hello_records: Array TLSPlaintext = TLSPlaintext::split_fragments(ContentType::handshake(), handshake_bytes);

        pure $ ClientHelloState {
            client_hello_records: client_hello_records,
            key_agreement: key_agreement
        }
    );

    send: [m: TLSConnection] ClientHelloState -> m ();
    send = |client_hello_state| (
        TLSPlaintext::send_plaintexts(client_hello_state.@client_hello_records)
    );
}

type ServerHelloState = unbox struct {
    protection: Protection,
    server_hello: ServerHello,
};

namespace ServerHelloState {
    make: [m: MonadError, ErrorType m = ErrMsg] ClientHelloState -> Array TLSPlaintext -> m ServerHelloState;
    make = |client_hello_state, server_hello_records| (
        let client_hello_handshake_bytes = *client_hello_state.@client_hello_records.join_fragments;
        let server_hello_handshake_bytes = *server_hello_records.join_fragments;
        let handshake: Handshake = *unmarshal_from_bytes(server_hello_handshake_bytes).from_result_t;
        if !handshake.is_server_hello {
            error $ "unexpected handshake type"
        };
        let server_hello: ServerHello = handshake.as_server_hello;
        eval debug_println("server_hello=" + server_hello.to_string);

        // key agreement
        let key_agreement = client_hello_state.@key_agreement;
        let key_share_entry = *server_hello._find_key_share_entry;
        let shared_secret: SharedSecret = *key_agreement.accept_server_share(key_share_entry).from_result_t;
        eval debug_println("shared_secret=" + shared_secret.to_string_hex);

        // construct protection
        let cipher_suite = server_hello.@cipher_suite;
        let protection = *Protection::make(cipher_suite).from_result_t;
        let protection = protection.add_transcript(client_hello_handshake_bytes);
        let protection = protection.add_transcript(server_hello_handshake_bytes);
        let protection = *protection.init_handshake(shared_secret).from_result_t;

        pure $ ServerHelloState {
            protection: protection,
            server_hello: server_hello,
        }
    );


    recv: [m: TLSConnection, ErrorType m = ErrMsg] ClientHelloState -> m ServerHelloState;
    recv = |client_hello_state| (
        let plaintext: TLSPlaintext = *TLSPlaintext::recv_plaintext;
        if !plaintext.@content_type.is_handshake {
            error $ "unexpected content type"
        };
        ServerHelloState::make(client_hello_state, [plaintext])
    );

    _find_key_share_entry: [m: MonadError, ErrorType m = ErrMsg] ServerHello -> m KeyShareEntry;
    _find_key_share_entry = |hello| (
        let opt = hello.@extensions.find(is_key_share).map(as_key_share);
        if opt.is_none { error $ "key_share not found" };
        let ks: KeyShare = opt.as_some;
        if !ks.is_ks_server_hello { error $ "not ks_server_hello" };
        let kse: KeyShareEntry = ks.as_ks_server_hello.@server_share;
        pure $ kse
    );

}


type HandshakeState = unbox union {
    empty: (),
    client_hello_sent: ClientHelloState,
    server_hello_received: ServerHelloState,
};

namespace HandshakeState {
}

