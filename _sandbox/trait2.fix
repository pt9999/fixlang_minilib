module Main;

trait a: Default {
    default: a;
}

trait b: TBool {
    type IfElse b t f;
}

type TTrue = struct {};

impl TTrue: Default {
    default = TTrue {};
}

impl TTrue: ToString {
    to_string = "true";
}

impl TTrue: TBool {
    type IfElse TTrue t f = t;
}

type TFalse = struct {};

impl TFalse: Default {
    default = TFalse {};
}

impl TFalse: ToString {
    to_string = "false";
}

impl TFalse: TBool {
    type IfElse TFalse t f = f;
}

type Wrap a = struct {};

type EqPred a b c = struct {};

impl EqPred a b c: Default {
    default = EqPred {};
}

trait t: TPredicate {
    type PredVal t;
    pred_val: t -> Bool;
}

impl EqPred a (Wrap a) c: TPredicate {
    type PredVal (EqPred a (Wrap a) c) = TTrue;
    pred_val = |_| true;
}

impl EqPred a b (Wrap c): TPredicate {
    type PredVal (EqPred a b (Wrap c)) = TFalse;
    pred_val = |_| false;
}

type EqType a b = EqPred a (Wrap b) (Wrap ());

//type IfEq a b t f = IfElse (PredVal (EqType a b)) t f; // error

test_eqtype: IO ();
test_eqtype = (
    let p: EqType I64 I64 = default;
    println("EqType I64 I64 = " + p.pred_val.to_string);;
    let p: EqType I64 I32 = default;
    println("EqType I64 I32 = " + p.pred_val.to_string);;
    let q: IfElse (PredVal (EqType I64 I32)) String (Option I64) = some(1);
    pure()
);

trait l: TList {
    type TContains l a;
    type TAdd l a;
    type TRemove l a;
}

type TNull = struct {};

impl TNull: Default {
    default = TNull {};
}

impl TNull: TList {
    type TContains TNull a = TFalse;
    type TAdd TNull a = TCons a TNull;
    type TRemove TNull a = TNull;
}

type TCons car cdr = struct {};

impl TCons car cdr: Default {
    default = TCons {};
}

impl [cdr: TList] TCons car cdr: TList {
    type TContains (TCons car cdr) a = IfElse (PredVal (EqType car a)) TTrue (TContains cdr a);
    type TAdd (TCons car cdr) a = IfElse (PredVal (EqType car a)) (TCons car cdr) (TCons car (TAdd cdr a));
    type TRemove (TCons car cdr) a = IfElse (PredVal (EqType car a)) cdr (TCons car (TRemove cdr a));
}

show_contains: [l: TList] a -> String -> l -> IO ();
show_contains = |_, label, _| (
    let b: TContains l a = default;
    println(label + ": " + b.to_string)
);

tadd: [l: TList, r: Default, TAdd l a = r] a -> l -> TAdd l a;
tadd = |_, _| default;

tremove: [l: TList, r: Default, TRemove l a = r] a -> l -> TRemove l a;
tremove = |_, _| default;

test_tlist: IO () = (
    let l: TCons U8 (TCons I32 TNull) = default;
    l.show_contains(123, "contains([U8, I32], I64)");;
    l.show_contains(123_I32, "contains([U8, I32], I32)");;
    let l = l.tadd(123);
    let l = l.tremove(234_I32);
    l.show_contains(123, "contains([U8, I64], I64)");;
    l.show_contains(123_I32, "contains([U8, I64], I32)");;
    pure()
);

//------------------------------------
main: IO ();
main = (
    //test_eqtype;;
    test_tlist;;
    pure()
);
