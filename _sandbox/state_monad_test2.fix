// TODO: use UnitTest
module Main;

import Sandbox.StateT;

type App = unbox struct {
    users: Array String,
    pages: Array String
};

type [m: * -> *] StateApp m = StateT m App;

namespace App {
    empty: App;
    empty = App {
        users: Array::empty(0),
        pages: Array::empty(0)
    };

    get_app: [m: Monad] StateApp m App;
    get_app = get_state;

    mod_app: [m: Monad] StateApp m ();
    mod_app = mod_state;

    add_user: [m: Monad] String -> StateApp m ();
    add_user = |user| (
        mod_state(mod_users(push_back(user)))
    );

    add_page: [m: Monad] String -> StateApp m ();
    add_page = |page| (
        mod_state(mod_pages(push_back(page)))
    );

    get_users: [m: Monad] StateApp m (Array String);
    get_users = get_state.map(@users);

    get_pages: [m: Monad] StateApp m (Array String);
    get_pages = get_state.map(@pages);

    print_users: StateApp IO ();
    print_users = do {
        let users = *get_users;
        to_state $ do {
            let _ = *println("---users---");
            users.to_iter.fold_m((), |_, u| println(u))
        }
    };

    print_pages: StateApp IO ();
    print_pages = do {
        let pages = *get_pages;
        to_state $ do {
            let _ = *println("---pages---");
            pages.to_iter.fold_m((), |_, p| println(p))
        }
    };

    register_users: [m: Monad] StateApp m ();
    register_users = do {
        let _ = *add_user("taro");
        let _ = *add_user("jiro");
        let _ = *add_user("saburo");
        pure()
    };

    create_user_pages: [m: Monad] StateApp m ();
    create_user_pages = do {
        let users = *get_users;
        users.to_iter.fold_m(
            (), |_, user|
            add_page(user + "'s page")
        )
    };

    save_file: String -> StateApp IOFail ();
    save_file = |path| (
        let users = *get_users;
        let pages = *get_pages;
        to_state $ do {
            if path == "" { throw $ "Empty path" };
            let path = Path::parse(path);
            if path.is_none { throw $ "Path::parse failed" };
            let path = path.as_some;
            let fh = *open_file(path, "w");
            let _ = *users.to_iter.join(",").write_string(fh);
            let _ = *pages.to_iter.join(",").write_string(fh);
            let _ = *close_file(fh).lift;
            pure()
        }
    );
}

test_state: IO ();
test_state = do {
    let _ = *println("=== test_state ===");
    let m = do {
        let _ = *register_users;
        get_users
    };
    let (users,_) = *m.run_state(App::empty);  // App 以外を指定するとpanicする
    let _ = *println(users.to_iter.join(","));
    pure()
};

stateio_func: StateApp IO ();
stateio_func = do {
    let _ = *print_users;
    let _ = *print_pages;
    let _ = *register_users;
    let _ = *create_user_pages;
    let _ = *print_users;
    let _ = *print_pages;
    pure ()
};


test_stateio: IO ();
test_stateio = do {
    let _ = *println("=== test_stateio ===");
    let (_,s) = *stateio_func.run_state(App::empty);
    pure()
};

stateiof_func: () -> StateApp IOFail ();
stateiof_func = |_| do {
    let lift_state = |lft, st| make_state_monad $ |s| st.run_state(s).lft;
    //let lift_state = |lft, st| make_state_monad $ |s| st.run_state(s).lft; // コメントを外すとなぜかエラーになる
    let _ = *stateio_func.lift_state(lift);
    let _ = *save_file("");
    let _ = *println("completed.").lift.to_state;
    pure()
};

test_stateiof: IO ();
test_stateiof = do {
    let _ = *println("=== test_stateiof ===").lift;
    let (_,s) = *stateiof_func().run_state(App::empty);
    pure()
}.try(|err| println("ERROR: " + err));

main: IO ();
main = (
    let _ = *test_state;
    let _ = *test_stateio;
    let _ = *test_stateiof;
    pure()
);
