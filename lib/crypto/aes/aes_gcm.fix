// Galois/Counter Mode (GCM) for AES
//
// Implemented from specification of NIST Special Publication 800-38D:
// Recommendation for Block Cipher Modes of Operation: Galois/Counter Mode (GCM) and GMAC
// https://doi.org/10.6028/NIST.SP.800-38D
module Minilib.Crypto.AES.GCM;


import Minilib.Encoding.Binary;
import Minilib.Crypto.AES;
import Minilib.Text.Hex;
//import Minilib.Thread.Time;

// A block is a bit string of 128 bits.
// We use two U64 as a representation.
// When converted to a bit string, it is encoded in big-endian.
// If a byte sequence is encoded to a bit string, MSB of the first byte becomes the leftmost bit.
type Block = unbox struct {
  hi: U64,
  lo: U64
};

impl Block: Eq {
    eq = |a, b| a.@hi == b.@hi && a.@lo == b.@lo;
}

impl Block: ToStringHex {
    to_string_hex = |block| block.@hi.to_string_hex + block.@lo.to_string_hex;
}

impl Block: ToString {
    to_string = to_string_hex;
}

impl Block: FromBytes {
    from_bytes = |bytes| (
        if bytes.get_size != 16 { err $ "invalid size" };
        ok $ bytes.get_block_be(0)
    );
}

impl Block: ToBytes {
    to_bytes = |block| (
        Array::fill(16, 0_U8).set_block_be(0, block)
    );
}

namespace Block {
    // `Block::make(hi, lo)` creates a 128 bit block.
    make: U64 -> U64 -> Block;
    make = |hi, lo| (
        Block { hi: hi, lo: lo }
    );

    // `buf.get_block_be(i)` reads a 128 bit block in big endian at position `i` in array `buf`.
    get_block_be: I64 -> Array U8 -> Block;
    get_block_be = |i, array| (
        let hi = array.get_u64_be(i);
        let lo = array.get_u64_be(i + 8);
        Block { hi: hi, lo: lo }
    );

    // `buf.set_block_be(i, block)` writes a 128 bit block in big endian at position `i` in array `buf`.
    set_block_be: I64 -> Block -> Array U8 -> Array U8;
    set_block_be = |i, block, array| (
        array.set_u64_be(i, block.@hi)
             .set_u64_be(i + 8, block.@lo)
    );

    // 6.1 Basic operation and functions

    // A zero block.
    _zero: Block;
    _zero = Block { hi: 0_U64, lo: 0_U64 };

    // Calculates bitwise XOR of two 128-bit blocks.
    _bit_xor: Block -> Block -> Block;
    _bit_xor = |x, y| (
        let hi = x.@hi.bit_xor(y.@hi);
        let lo = x.@lo.bit_xor(y.@lo);
        x.set_hi(hi).set_lo(lo)
    );

    // Shifts a 128-bit block to right by 1 bit.
    _shift_right_1: Block -> Block;
    _shift_right_1 = |x| (
        let hi = x.@hi.shift_right(1_U64);
        let lo = x.@lo.shift_right(1_U64);
        let lo = if x.@hi.bit_and(1_U64) == 0_U64 { lo } else { lo.bit_or(1_U64.shift_left(63_U64)) };
        x.set_hi(hi).set_lo(lo)
    );

    _u64_to_bitstring: U64 -> Array U8;
    _u64_to_bitstring = |u64| (
        Array::fill(8, 0_U8).set_u64_be(0, u64)
    );

    // 6.2 Incrementing function
    _inc_32: Block -> Block;
    _inc_32 = |x| (
        let lo = x.@lo.bit_and(0xffffffff00000000_U64)
                .bit_or((x.@lo.to_U32 + 1_U32).to_U64);
        x.set_lo(lo)
    );

    // 6.3 Multiplication operation of Blocks

    // Multiplication operation of Blocks
    _mul: Block -> Block -> Block;
    //_mul = _clmul_u128_u128_reduce;     // a bit fast
    _mul = _mul_bitwise;              // a bit slow, but not so bad

    // Multiplication operation of Blocks using bitwise-xor and reducing by the reduction polynomial `_R`.
    _mul_bitwise: Block -> Block -> Block;
    _mul_bitwise = |x, y| (
        let z = _zero;
        let v = y;
        loop(
            (z, v, 0), |(z, v, i)|
            if i > 127 { break $ z };
            let z = if x._is_bit_set(i) { z._bit_xor(v) } else { z };
            let v = if v._is_bit_set(127) { v._shift_right_1._bit_xor(_R) } else { v._shift_right_1 };
            continue $ (z, v, i + 1)
        )
    );

    // `x._is_bit_set(i)` returns true if the bit `i` is set.
    // NOTE that the bit 0 is the leftmost bit (ie. MSB) of the block.
    _is_bit_set: I64 -> Block -> Bool;
    _is_bit_set = |i, x| (
        if i < 64 { x.@hi.bit_and(1_U64.shift_left((63 - i).to_U64)) != 0_U64 };
        if i < 128 { x.@lo.bit_and(1_U64.shift_left((127 - i).to_U64)) != 0_U64 };
        false
    );

    // The reduction polynomial `R = 11100011 || 0^120`.
    // This is the primitive polynomial of GF(2^128).
    _R: Block;
    _R = Block { hi: 0b11100001_U64.shift_left(56_U64), lo: 0_U64 };

    // 6.4 GHASH function

    // Calculates GHASH() function.
    _GHASH: Block -> Iterator Block -> Block;
    _GHASH = |h, xs| (
        xs.fold(_zero, |y, x| y._bit_xor(x)._mul(h))
    );

    // Converts a byte array to an iterator of 128-bit blocks.
    _bytes_to_blocks: Array U8 -> Iterator Block;
    _bytes_to_blocks = |bytes| (
        eval assert(|_| "invalid byte size", bytes.get_size % 16 == 0);
        let n = bytes.get_size / 16;
        Iterator::range(0, n).map(|i|
            bytes.get_block_be(i * 16)
        )
    );

    // 6.5 GCTR function

    // Calculates GCTR() function.
    _GCTR: (Block -> Block) -> Block -> Array U8 -> Array U8;
    _GCTR = |cipher, initial_counter_block, x_bytes| (
        if x_bytes.is_empty { x_bytes };
        let n = (x_bytes.get_size + 15) / 16;   // ceil(len(x) / 128)
        let padding = n * 16 - x_bytes.get_size;
        let x_bytes_pad = if padding == 0 { x_bytes }
        else { x_bytes.append(Array::fill(padding, 0_U8)) };
        let y_bytes_pad = Array::fill(x_bytes_pad.get_size, 0_U8);
        let cb = initial_counter_block;
        let y_bytes_pad = loop(
            (y_bytes_pad, cb, 0), |(y_bytes_pad, cb, i)|
            if i >= n { break $ y_bytes_pad };
            let x = x_bytes_pad.get_block_be(i * 16);
            let y = x._bit_xor(cipher(cb));
            let y_bytes_pad = y_bytes_pad.set_block_be(i * 16, y);
            let cb = cb._inc_32;
            continue $ (y_bytes_pad, cb, i + 1)
        );
        let y_bytes = if padding == 0 { y_bytes_pad }
        else { y_bytes_pad.get_sub(0, y_bytes_pad.get_size - padding) };
        y_bytes
    );
}

namespace GCM {

    // Converts an AES structure to a cipher.
    // NOTE: aes.decrypt_block will not be used in AES-GCM.
    _to_cipher: AES -> (Block -> Block);
    _to_cipher = |aes, x| (
        let plaintext = x.to_bytes;
        let ciphertext = aes.encrypt_block(plaintext);
        //let ciphertext = timeit("aes", |_| aes.encrypt_block(plaintext))._unsafe_perform;
        ciphertext.from_bytes.as_ok
    );

    // 7.1 Algorithm for the Authenticated Encryption Function

    // Performs authenticated encryption.
    // Input:
    // - cipher: 128-bit block cipher
    // - iv: initialization vector, typically 96 bits
    // - plaintext: a byte sequence which will be encrypted
    // - auth_data: a byte sequence which will not be encrypted
    // - len_t: tag length in bits
    // Output: (c, t)
    // - c: ciphertext
    // - t: authentication tag
    gcm_ae: (Block -> Block) -> Array U8 -> Array U8 -> Array U8 -> I64 -> (Array U8, Array U8);
    gcm_ae = |cipher, iv, plaintext, auth_data, len_t| (
        // h: the hash subkey
        let h: Block = cipher(_zero);
        // j0: the pre-counter block
        let j0: Block  = _get_j0(h, iv);

        let c: Array U8 = _GCTR(cipher, j0._inc_32, plaintext);
        let t = _get_tag(cipher, h, j0, c, auth_data, len_t);
        (c, t)
    );

    // get the pre-counter block
    _get_j0: Block -> Array U8 -> Block;
    _get_j0 = |h, iv| (
        if iv.get_size == 96 / 8 {
            iv.append([0_U8, 0_U8, 0_U8, 1_U8]).get_block_be(0)
        } else {
            let len_iv = iv.get_size * 8;
            let s = 128 * ((len_iv + 127) / 128) - len_iv;
            let zeros = Array::fill((s + 64) / 8, 0_U8);    // NOTE that `s % 8 == 0`.
            let len_iv_str = _u64_to_bitstring(len_iv.to_U64);
            _GHASH(h, iv.append(zeros).append(len_iv_str)._bytes_to_blocks)
        }
    );

    // get the authentication tag
    _get_tag: (Block -> Block) -> Block -> Block -> Array U8 -> Array U8 -> I64 -> Array U8;
    _get_tag = |cipher, h, j0, c, auth_data, len_t| (
        let len_c = c.get_size * 8;
        let u = 128 * ((len_c + 127) / 128) - len_c;
        let len_a = auth_data.get_size * 8;
        let v = 128 * ((len_a + 127) / 128) - len_a;
        // NOTE that `u % 8 == 0` and `v % 8 == 0`.
        //eval debug_eprintln("len_a + v =" + (len_a + v).to_string);
        //eval debug_eprintln("len_c + u =" + (len_c + u).to_string);
        let s: Block = _GHASH(h,
            auth_data
            .append(Array::fill(v / 8, 0_U8))
            .append(c)
            .append(Array::fill(u / 8, 0_U8))
            .append(_u64_to_bitstring(len_a.to_U64))
            .append(_u64_to_bitstring(len_c.to_U64))
            ._bytes_to_blocks
        );
        let t: Array U8 = _GCTR(cipher, j0, s.to_bytes);
        let t = if len_t < t.get_size * 8 { t.get_sub(0, len_t / 8) } else { t };
        t
    );

    // 7.2 Algorithm for the Authenticated Decryption Function

    // Performs authenticated decryption.
    // Input:
    // - cipher: 128-bit block cipher
    // - iv: initialization vector, typically 96 bits
    // - ciphertext: a byte sequence which will be decrypted
    // - auth_data: a byte sequence which will not be encrypted
    // - tag: authentication tag
    // Output: ok(plaintext) or err(ErrMsg)
    // - ok(plaintext): decrypted byte sequence
    // - err(ErrMsg): inauthenticity
    gcm_ad: (Block -> Block) -> Array U8 -> Array U8 -> Array U8 -> Array U8 -> I64 -> Result ErrMsg (Array U8);
    gcm_ad = |cipher, iv, ciphertext, auth_data, tag, len_t| (
        // 5.2.1.1 Input Data
        if ciphertext.get_size > (1.shift_left(39) - 256) / 8 {
            err $ "ciphertext too long"
        };
        if auth_data.get_size > (U64::maximum / 8_U64).to_I64 {
            err $ "auth_data too long"
        };
        if iv.get_size > (U64::maximum / 8_U64).to_I64 {
            err $ "iv too long"
        };
        if iv.get_size < 1 {
            err $ "iv too short"
        };
        if tag.get_size * 8 != len_t {
            err $ "tag length mismatch"
        };
        // h: the hash subkey
        let h: Block = cipher(_zero);
        // j0: the pre-counter block
        let j0: Block  = _get_j0(h, iv);
        // p: plaintext
        let p: Array U8 = _GCTR(cipher, j0._inc_32, ciphertext);
        let t = _get_tag(cipher, h, j0, ciphertext, auth_data, len_t);
        if tag != t {
            err $ "authentication tag mismatch"
        };
        ok $ p
    );
}

// Carryless Multiplication
// NOTE: the bit order is reversed, ie. bit 0 is MSB, bit (n-1) is LSB
namespace CLMul {
    _clmul_u8_u8_table: Array U16;
    _clmul_u8_u8_table = Array::from_map(256 * 256, |pos|
        let a = pos.shift_left(0).bit_and(0xff00).to_U16;
        let b = pos.shift_left(8).bit_and(0xff00).to_U16;
        let c = 0_U16;
        loop(
            (c, a, 0), |(c, a, i)|
            if i >= 8 { break $ c };
            let c = if b.shift_right((15 - i).to_U16).bit_and(1_U16) != 0_U16 {
                c.bit_xor(a)
            } else { c };
            let a = a.shift_right(1_U16);
            continue $ (c, a, i + 1)
        )
    );

    // Performs carryless multiplication of U8 and U8.
    _clmul_u8_u8: U8 -> U8 -> U16;
    _clmul_u8_u8 = |a, b| (
        let pos = a.to_I64.shift_left(8).bit_or(b.to_I64);
        _clmul_u8_u8_table.@(pos)
    );

    // Performs carryless multiplication of U16 and U16.
    // NOTE: the bit order is reversed, ie. bit 0 is MSB, bit (n-1) is LSB.
    _clmul_u16_u16: U16 -> U16 -> U32;
    _clmul_u16_u16 = |x, y| (
        let x1 = x.to_U8;
        let x0 = x.shift_right(8_U16).to_U8;
        let y1 = y.to_U8;
        let y0 = y.shift_right(8_U16).to_U8;
        let z2 = _clmul_u8_u8(x1, y1);
        let z0 = _clmul_u8_u8(x0, y0);
        let z1 = z2.bit_xor(z0).bit_xor(_clmul_u8_u8(x1.bit_xor(x0), y1.bit_xor(y0)));
        let z = z2.to_U32.bit_xor(z1.to_U32.shift_left(8_U32)).bit_xor(z0.to_U32.shift_left(16_U32));
        z
    );

    // Performs carryless multiplication of U32 and U32.
    // NOTE: the bit order is reversed, ie. bit 0 is MSB, bit (n-1) is LSB.
    _clmul_u32_u32: U32 -> U32 -> U64;
    _clmul_u32_u32 = |x, y| (
        let x1 = x.to_U16;
        let x0 = x.shift_right(16_U32).to_U16;
        let y1 = y.to_U16;
        let y0 = y.shift_right(16_U32).to_U16;
        let z2 = _clmul_u16_u16(x1, y1);
        let z0 = _clmul_u16_u16(x0, y0);
        let z1 = z2.bit_xor(z0).bit_xor(_clmul_u16_u16(x1.bit_xor(x0), y1.bit_xor(y0)));
        let z = z2.to_U64.bit_xor(z1.to_U64.shift_left(16_U64)).bit_xor(z0.to_U64.shift_left(32_U64));
        z
    );

    // Performs carryless multiplication of U64 and U64. `z` is used as a buffer for setting the result.
    // NOTE: the bit order is reversed, ie. bit 0 is MSB, bit (n-1) is LSB.
    _clmul_u64_u64: U64 -> U64 -> Block -> Block;
    _clmul_u64_u64 = |x, y, z| (
        let x1 = x.to_U32;
        let x0 = x.shift_right(32_U64).to_U32;
        let y1 = y.to_U32;
        let y0 = y.shift_right(32_U64).to_U32;
        let z2 = _clmul_u32_u32(x1, y1);
        let z0 = _clmul_u32_u32(x0, y0);
        let z1 = z2.bit_xor(z0).bit_xor(_clmul_u32_u32(x1.bit_xor(x0), y1.bit_xor(y0)));
        let z = z.set_hi(z0.bit_xor(z1.shift_right(32_U64)))
                .set_lo(z2.bit_xor(z1.shift_left(32_U64)));
        z
    );

    // Performs multiplication operation on blocks.
    // ie. clmul(u128, u128) and reduce the result by the reduction polynomial `R = 11100001 || 0^120`.
    // NOTE: the bit order is reversed, ie. bit 0 is MSB, bit (n-1) is LSB.
    _clmul_u128_u128_reduce: Block -> Block -> Block;
    _clmul_u128_u128_reduce = |x, y| (
        let x1 = x.@lo;
        let x0 = x.@hi;
        let y1 = y.@lo;
        let y0 = y.@hi;
        let z2 = x; // reuse as a temporary buffer
        let z1 = Block::_zero;
        let z0 = y; // reuse as a temporary buffer
        let z2 = _clmul_u64_u64(x1, y1, z2);
        let z0 = _clmul_u64_u64(x0, y0, z0);
        let z1 = _clmul_u64_u64(x1.bit_xor(x0), y1.bit_xor(y0), z1)
                ._bit_xor(z2)._bit_xor(z0);
        let z2 = z2.mod_hi(bit_xor(z1.@lo));
        let z0 = z0.mod_lo(bit_xor(z1.@hi));
        // reduce the result by R = 0b11100001 * 2^120
        let t  = z2.@lo;
        let z0 = z0.mod_lo(bit_xor(t
                                .bit_xor(t.shift_right(1_U64))
                                .bit_xor(t.shift_right(2_U64))
                                .bit_xor(t.shift_right(7_U64))));
        let z2 = z2.mod_hi(bit_xor(t.shift_left(63_U64)
                                .bit_xor(t.shift_left(62_U64))
                                .bit_xor(t.shift_left(57_U64))));
        let t = z2.@hi;
        let z0 = z0.mod_hi(bit_xor(t
                                .bit_xor(t.shift_right(1_U64))
                                .bit_xor(t.shift_right(2_U64))
                                .bit_xor(t.shift_right(7_U64))));
        let z0 = z0.mod_lo(bit_xor(t.shift_left(63_U64)
                                .bit_xor(t.shift_left(62_U64))
                                .bit_xor(t.shift_left(57_U64))));
        z0
    );
}
