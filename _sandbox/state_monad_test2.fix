// TODO: use UnitTest
module Main;

import Minilib.Monad.State;
import Minilib.Monad.IO;
import Minilib.Monad.Error;
import Minilib.Monad.FunctorM;

type App = unbox struct {
    users: Array String,
    pages: Array String
};

type StateApp = StateT App;

namespace App {
    empty: App;
    empty = App {
        users: Array::empty(0),
        pages: Array::empty(0)
    };

    get_app: [m: Monad] StateApp m App;
    get_app = get_state;

    mod_app: [m: Monad] StateApp m ();
    mod_app = mod_state;

    add_user: [m: Monad] String -> StateApp m ();
    add_user = |user| (
        mod_state(mod_users(push_back(user)))
    );

    add_page: [m: Monad] String -> StateApp m ();
    add_page = |page| (
        mod_state(mod_pages(push_back(page)))
    );

    get_users: [m: Monad] StateApp m (Array String);
    get_users = get_state.map(@users);

    get_pages: [m: Monad] StateApp m (Array String);
    get_pages = get_state.map(@pages);

    print_users: [m: MonadIO] StateApp m ();
    print_users = do {
        let users = *get_users;
        eval *println("---users---").lift_io;
        users.foreach_m(println).lift_io
    };

    print_pages: [m: MonadIO] StateApp m ();
    print_pages = do {
        let pages = *get_pages;
        eval *println("---pages---").lift_io;
        pages.foreach_m(println).lift_io
    };

    register_users: [m: Monad] StateApp m ();
    register_users = do {
        eval *add_user("taro");
        eval *add_user("jiro");
        eval *add_user("saburo");
        pure()
    };

    create_user_pages: [m: Monad] StateApp m ();
    create_user_pages = do {
        let users = *get_users;
        users.to_iter.fold_m(
            (), |_, user|
            add_page(user + "'s page")
        )
    };

    save_file: [m: MonadIOFail, m: MonadError] String -> StateApp m ();
    save_file = |path| (
        let users = *get_users;
        let pages = *get_pages;
        if path == "" { error $ "Empty path" };
        let path = Path::parse(path);
        if path.is_none { error $ "Path::parse failed" };
        let path = path.as_some;
        write_file_string(path,
            users.to_iter.join(",") + "\n" +
            pages.to_iter.join(",")
        ).lift_iofail
    );
}

test_state: IO ();
test_state = (
    eval *println("=== test_state ===");
    let m = do {
        eval *register_users;
        get_users
    };
    let (users,_) = *m.run_state_t(App::empty);
    //let (users,_) = *m.run_state_t(123);  // App 以外を指定すると型エラーになる
    eval *println(users.to_iter.join(","));
    pure()
);

stateio_func: [m: MonadIO] StateApp m ();
stateio_func = (
    eval *pure();
    eval *print_users;
    eval *print_pages;
    eval *register_users;
    eval *create_user_pages;
    eval *print_users;
    eval *print_pages;
    pure ()
);

test_stateio: IO ();
test_stateio = (
    eval *println("=== test_stateio ===");
    let (_,s) = *stateio_func.run_state_t(App::empty);
    pure()
);

stateiof_func: StateApp IOFail ();
stateiof_func = (
    eval *pure();
    eval *stateio_func;
    //eval *save_file("tmp.state_monad_test2.txt");
    eval *save_file("");    // "ERROR: Empty path" となる
    eval *println("completed.").lift_io;
    pure()
);

test_stateiof: IO ();
test_stateiof = do {
    eval *println("=== test_stateiof ===").lift;
    let (_,s) = *stateiof_func.run_state_t(App::empty);
    pure()
}.try(|err| println("ERROR: " + err));

main: IO ();
main = (
    eval *test_state;
    eval *test_stateio;
    eval *test_stateiof;
    pure()
);
