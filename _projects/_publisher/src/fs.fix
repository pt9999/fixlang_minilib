module Publisher.Fs;

import Minilib.Common.IORef;
import Minilib.IO.FileSystem;
import Minilib.IO.Path;
import Minilib.Monad.Reader;
import Minilib.Monad.Error;
import Minilib.Monad.IO;

import Publisher.Common;

trait MonadFs = Functor + MonadIOFail + MonadErrorIF + MonadFsIF;

trait [m: *->*] m: MonadFsIF {
    fs_file_exists: Path -> m Bool;
    fs_directory_exists: Path -> m Bool;
    fs_chdir: Path -> m ();
    fs_realpath: Path -> m Path;
    fs_read_file_string: Path -> m String;
    fs_write_file_string: Path -> String -> m ();
}

impl IOFail: MonadFsIF {
    fs_file_exists = |path| file_exists(path).lift;
    fs_directory_exists = |path| directory_exists(path).lift;
    fs_chdir = |path| chdir(path);
    fs_realpath = |path| realpath(path);
    fs_read_file_string = |path| read_file_string(path);
    fs_write_file_string = |path, contents| write_file_string(path, contents);
}

trait api: FileSystemApi {
    fsapi_file_exists: IORef api -> Path -> IOFail Bool;
    fsapi_directory_exists: IORef api -> Path -> IOFail Bool;
    fsapi_chdir: IORef api -> Path -> IOFail ();
    fsapi_realpath: IORef api -> Path -> IOFail Path;
    fsapi_read_file_string: IORef api -> Path -> IOFail String;
    fsapi_write_file_string: IORef api -> Path -> String -> IOFail ();
}

namespace FileSystemApi {
    run_fsapi: [e: HasFileSystemApi, HasFileSystemApi::ApiType e = api, api: FileSystemApi] (IORef api -> func) -> ReaderT e IOFail func;
    run_fsapi = |get_func| (
        let env = *ask;
        let ioref_api = env.get_file_system_api;
        pure $ ioref_api.get_func
    );
}

trait e: HasFileSystemApi {
    type ApiType e;
    get_file_system_api: e -> IORef (ApiType e);
}

impl [e: HasFileSystemApi, HasFileSystemApi::ApiType e = api, api: FileSystemApi] ReaderT e IOFail: MonadFsIF {
    fs_file_exists = |path| (
        (*fsapi_file_exists.run_fsapi)(path).lift_iofail
    );
    fs_directory_exists = |path| (
        (*fsapi_directory_exists.run_fsapi)(path).lift_iofail
    );
    fs_chdir = |path| (
        (*fsapi_chdir.run_fsapi)(path).lift_iofail
    );
    fs_realpath = |path| (
        (*fsapi_realpath.run_fsapi)(path).lift_iofail
    );
    fs_read_file_string = |path| (
        (*fsapi_read_file_string.run_fsapi)(path).lift_iofail
    );
    fs_write_file_string = |path, contents| (
        (*fsapi_write_file_string.run_fsapi)(path, contents).lift_iofail
    );
}

