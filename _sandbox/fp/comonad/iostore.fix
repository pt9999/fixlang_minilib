module Minilib.Comonad.IOStore;

import Minilib.Common.Common;
import Minilib.Trait.Comonad;

//------------------------------------------------------
// IOStore comonad
// (Specialized store comonad whose state is IOState)

type IOStore a = unbox struct {
    data: (IOState, IOState -> a)
};

namespace IOStore {
    make: (IOState, IOState -> a) -> IOStore a;
    make = |data| IOStore { data: data };

    from_iostate: IOState -> IOStore IOState;
    from_iostate = |ios| IOStore::make $ (ios, id);

    run_io_store: IOStore a -> (IOState, IOState -> a);
    run_io_store = @data;
}

impl IOStore: Functor {
    map = |g, store| (
        let (ios, f) = store.@data;
        IOStore::make $ (ios, f.map(g))
    );
}

impl IOStore: Comonad {
    extract = |store| (
        let (ios, f) = store.run_io_store;
        f(ios)
    );
    extend = |g, store| (
        let (ios, fb) = store.run_io_store;
        let fa = |ios| g $ IOStore::make $ (ios, fb);
        IOStore::make $ (ios, fa)
    );
}