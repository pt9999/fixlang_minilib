module Minilib.Type.TNat;

import Minilib.Type.TBool;

trait n: TNat {
    type TIsZero n;
    type TPred n;
    type TNatAdd n m;
    type TNatSub n m;
    type TNatMul n m;
}

type TZero = unbox struct {};

impl TZero: Zero {
    zero = TZero {};
}

impl TZero: ToString {
    to_string = |_| "TZero";
}

impl TZero: TNat {
    type TIsZero TZero = TTrue;
    type TPred TZero = TZero;
    type TNatAdd TZero m = m;
    type TNatSub TZero m = TZero;
    type TNatMul TZero m = TZero;
}

type TSucc n = unbox struct {};

impl TSucc n: Zero {
    zero = TSucc {};
}

impl [n: ToString, n: Zero] TSucc n: ToString {
    to_string = |_| "TSucc (" + (zero: n).to_string + ")";
}

impl [n: TNat] TSucc n: TNat {
    type TIsZero (TSucc n) = TFalse;
    type TPred (TSucc n) = n;
    type TNatAdd (TSucc n) m = TNatAdd n (TSucc m);
    type TNatSub (TSucc n) m = TNatSub n (TPred m);   // requires m: TNat
    type TNatMul (TSucc n) m = TNatAdd (TNatMul n m) m;   // requires m: TNat
}

type TOne = TSucc TZero;
type TTwo = TSucc TOne;
//type TFour = TNatAdd TTwo TTwo;     // error: Associated type is not allowed in the right-hand side of a type alias.
//type TEight = TNatAdd TFour TFour;
//type TSixteen = TNatAdd TEight TEight;


