module Sandbox.StateMonad;

import Debug;

trait [m: * -> *] m: FromIO {
    from_io: IO a -> m a;
}

trait [m: * -> *] m: FromIOFail {
    from_iofail: IOFail a -> m a;
}

//-------------------------------------------------------
// State Monad
//-------------------------------------------------------

type State s a = unbox struct { _data: s -> (a, s) };

namespace State {
    make_state_monad: (s -> (a, s)) -> State s a;
    make_state_monad = |f| State { _data: f };

    run_state: s -> State s a -> (a, s);
    run_state = |s, ma| (ma.@_data) (s);

    get_state_: State s s;
    get_state_ = make_state_monad $ |s| (s, s);

    put_state_: s -> State s ();
    put_state_ = |s| make_state_monad $ |_| ((), s);

    mod_state_: (s -> s) -> State s ();
    mod_state_ = |f| put_state_ $ f $ *get_state_;
}

impl State s: Monad {
    pure = |a| make_state_monad $ |s| (a, s);
    bind = |f, ma| make_state_monad $ |s|
        let (a, ss) = ma.run_state(s);
        f(a).run_state(ss);
}

impl State s: Functor {
    map = |f, ma| make_state_monad $ |s|
        let (a, ss) = ma.run_state(s);
        (f(a), ss);
}

// This is a weak typing because `[m: GetPutState]` does not have `s`.
trait [m: * -> *] m: GetPutState {
    get_state: m s;
    put_state: s -> m ();
}

trait StateMonad = Functor + Monad + GetPutState;

impl State s: GetPutState {
    get_state = State::get_state_;
    put_state = State::put_state_;
}

// This is a weak typing because `[m: StateMonad]` does not have `s`.
mod_state: [m: StateMonad] (s -> s) -> m ();
mod_state = |f| put_state $ f $ *get_state;

trait [m: * -> *] m: ToState {
    to_state: m a -> State s a;
}

/*
impl Option: ToState {
    to_state = |ma| make_state_monad $ |s| pure $ (*ma, s);
}

impl Result e: ToState {
    to_state = |ma| make_state_monad $ |s| pure $ (*ma, s);
}
*/

//-------------------------------------------------------
// StateIO Monad
//-------------------------------------------------------

type StateIO s a = unbox struct { _data: s -> IO (a, s) };

namespace StateIO {
    make_stateio_monad: (s -> IO (a, s)) -> StateIO s a;
    make_stateio_monad = |f| StateIO { _data: f };

    run_stateio: s -> StateIO s a -> IO (a, s);
    run_stateio = |s, ma| (ma.@_data) (s);
}

impl StateIO s: Functor {
    map = |f, ma| make_stateio_monad $ |s|
        let (a, ss) = *ma.run_stateio(s);
        pure $ (f(a), ss);
}

impl StateIO s: Monad {
    pure = |a| make_stateio_monad $ |s| pure $ (a, s);
    bind = |f, ma| make_stateio_monad $ |s|
        let (a, ss) = *ma.run_stateio(s);
        f(a).run_stateio(ss);
}

impl StateIO s: GetPutState {
    get_state = make_stateio_monad $ |s| pure $ (s, s);
    put_state = |s| make_stateio_monad $ |_| pure $ ((), s);
}

trait [m: * -> *] m: ToStateIO {
    to_stateio: m a -> StateIO s a;
}

impl State s: ToStateIO {
    to_stateio = |sa| make_stateio_monad $ |s| pure $ sa.run_state(s);
}

impl IO: ToStateIO {
    to_stateio = |ioa| make_stateio_monad $ |s| pure $ (*ioa, s);
}

impl StateIO s: FromIO {
    from_io = to_stateio;
}
trait StateIOMonad = StateMonad + FromIO;

//-------------------------------------------------------
// StateIOF Monad
//-------------------------------------------------------

type StateIOF s a = unbox struct { _data: s -> IOFail (a, s) };

namespace StateIOF {
    make_stateiof_monad: (s -> IOFail (a, s)) -> StateIOF s a;
    make_stateiof_monad = |f| StateIOF { _data: f };

    run_stateiof: s -> StateIOF s a -> IOFail (a, s);
    run_stateiof = |s, ma| (ma.@_data) (s);
}

impl StateIOF s: Functor {
    map = |f, ma| make_stateiof_monad $ |s|
        let (a, ss) = *ma.run_stateiof(s);
        pure $ (f(a), ss);
}

impl StateIOF s: Monad {
    pure = |a| make_stateiof_monad $ |s| pure $ (a, s);
    bind = |f, ma| make_stateiof_monad $ |s|
        let (a, ss) = *ma.run_stateiof(s);
        f(a).run_stateiof(ss);
}

impl StateIOF s: GetPutState {
    get_state = make_stateiof_monad $ |s| pure $ (s, s);
    put_state = |s| make_stateiof_monad $ |_| pure $ ((), s);
}

trait [m: * -> *] m: ToStateIOF {
    to_stateiof: m a -> StateIOF s a;
}

impl State s: ToStateIOF {
    to_stateiof = |sa| make_stateio_monad $ |s| pure $ sa.run_state(s);
}

impl StateIO s: ToStateIOF {
    to_stateiof = |sia| make_stateiof_monad $ |s| sia.run_stateio(s).lift;
}

impl IO: ToStateIOF {
    to_stateiof = |ioa| make_stateiof_monad $ |s| pure $ (*ioa.lift, s);
}

impl StateIOF s: FromIO {
    from_io = to_stateiof;
}

impl IOFail: ToStateIOF {
    to_stateiof = |iofa| make_stateiof_monad $ |s| pure $ (*iofa, s);
}
impl StateIOF s: FromIOFail {
    from_iofail = to_stateiof;
}
trait StateIOFMonad = StateIOMonad + FromIOFail;

impl Result ErrMsg: ToStateIOF {
    to_stateiof = |res| make_stateiof_monad $ |s| pure $ (*res.from_result, s);
}
