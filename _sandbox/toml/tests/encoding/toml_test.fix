module TomlTest;

import Minilib.Encoding.Toml;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;
import Minilib.Trait.Traversable;

test_tomltable_simple: TestCase;
test_tomltable_simple = (
    make_test("test_tomltable_simple") $ |_|
    let table = TomlTable::empty;
    let table = table.insert("key1", v_string("value1"));
    assert_equal("size", 1, table.get_size);;
    assert_equal("find key1", some $ v_string("value1"), table.find("key1"));;
    assert_equal("find key2", none(), table.find("key2"));;
    let table = table.erase("key1");
    assert_equal("size", 0, table.get_size);;
    assert_equal("find key1", none(), table.find("key1"));;
    pure()
);

test_tomltable_indexable_simple: TestCase;
test_tomltable_indexable_simple = (
    make_test("test_tomltable_indexable_simple") $ |_|
    let table = TomlTable::empty;
    let table = table["key1"].tset("value1");
    let val: String = *table["key1"].tget;
    assert_equal("eq", "value1", val);;
    let res: Result ErrMsg I64 = table["key1"].tget;
    assert_equal("eq", err $ "not an integer", res)
);

test_tomltable_indexable_multi: TestCase;
test_tomltable_indexable_multi = (
    make_test("test_tomltable_indexable_multi") $ |_|
    let table = TomlTable::empty;
    let table = table["key1"].tset("value1");
    let table = table["key2"]["key21"].tset("value21");
    let table = table["key2"]["key22"]["key221"].tset("value221");
    let table = table["key2"]["key23"]["0"]["key2301"].tset("value2301");
    let table = table["key2"]["key23"]["1"]["key2311"].tset("value2311");
    assert_equal("eq", "value1", *table["key1"].tget);;
    assert_equal("eq", "value21", *table["key2"]["key21"].tget);;
    assert_equal("eq", "value221", *table["key2"]["key22"]["key221"].tget);;
    let expected = [
        "key1 = \"value1\"",
        "[key2]",
        "key21 = \"value21\"",
        "[key2.key22]",
        "key221 = \"value221\"",
        "[[key2.key23]]",
        "key2301 = \"value2301\"",
        "[[key2.key23]]",
        "key2311 = \"value2311\"",
    ].to_iter.join("\n") + "\n";
    let actual = table.to_string;
    assert_equal("eq", expected, actual);;
    pure()
);

test_tomltable_indexable_multi2: TestCase;
test_tomltable_indexable_multi2 = (
    make_test("test_tomltable_indexable_multi2") $ |_|
    let table = TomlTable::empty;
    //let table = table["general"].tset(TomlTable::empty);
    let table = table["general"]["name"].tset("test");
    let name: String = *table["general"]["name"].tget;
    assert_equal("name", "test", name);;
    let table = table["general"]["version"].tset("0.0.0");
    //let table = table["build"].tset(TomlTable::empty);
    let table = table["build"]["files"].tset(["a", "b"]);
    let table = table["build"]["static_links"].tset([]: Array String);
    //let table = table["dependencies"].tset(v_array_table(TomlArray::empty));
    let table = table["dependencies"]["0"]["name"].tset("certain-project");
    let expected = [
        "[general]",
        "name = \"test\"",
        "version = \"0.0.0\"",
        "[build]",
        "files = [\"a\", \"b\"]",
        "static_links = []",
        "[[dependencies]]",
        "name = \"certain-project\"",
    ].to_iter.join("\n") + "\n";
    let actual = table.to_string;
    assert_equal("eq", expected, actual);;
    pure()
);

test_tomltable_indexable_multi3: TestCase;
test_tomltable_indexable_multi3 = (
    make_test("test_tomltable_indexable_multi3") $ |_|
    let table = TomlTable::empty;
    let general = TomlTable::empty;
    let general = general["name"].tset("test");
    let general = general["version"].tset("0.0.0");
    let table = table["general"].tset(general);
    let build = TomlTable::empty;
    let build = build["files"].tset(["a", "b"]);
    let build = build["static_links"].tset([]: Array String);
    let table = table["build"].tset(build);
    let dependencies = TomlArray::empty;
    let dep = TomlTable::empty;
    let dep = dep["name"].tset("certain-project");
    let dependencies = dependencies.push_back(dep);
    let table = table["dependencies"].tset(v_array_table(dependencies));    // あまりかっこよくない
    let expected = [
        "[general]",
        "name = \"test\"",
        "version = \"0.0.0\"",
        "[build]",
        "files = [\"a\", \"b\"]",
        "static_links = []",
        "[[dependencies]]",
        "name = \"certain-project\"",
    ].to_iter.join("\n") + "\n";
    let actual = table.to_string;
    assert_equal("eq", expected, actual);;
    pure()
);


test_tomlarray_simple: TestCase;
test_tomlarray_simple = (
    make_test("test_tomlarray_simple") $ |_|
    let array = TomlArray::empty;
    let array = array.set(2, v_string("a"));
    assert_equal("size", 3, array.get_size);;
    assert_equal("@(1)", v_none(), array.@(1));;
    assert_equal("@(2)", v_string("a"), array.@(2));;
    assert_equal("@(3)", v_none(), array.@(3));;
    assert_equal("to_iter", [v_none(), v_none(), v_string("a")], array.to_iter.to_array);;
    pure()
);

test_tomlval_eq: TestCase;
test_tomlval_eq = (
    make_test("test_tomlval_eq") $ |_|
    let arr = [
        v_none(),
        v_string("a"),
        v_string("b"),
        v_int(1),
        v_int(2),
        v_float(1.0),
        v_float(2.0),
        v_array(TomlArray::make $ [v_int(1)]),
        v_array(TomlArray::make $ [v_int(2)]),
        v_table(TomlTable::empty.insert("a", v_string("a"))),
        v_table(TomlTable::empty.insert("a", v_string("b"))),
        // TODO: add v_array_table
    ];
    let n = arr.get_size;
    range(0, n).foreach_m(|i|
        range(i, n).foreach_m(|j|
            let expected = i == j;
            let actual = arr.@(i) == arr.@(j);
            assert_equal("(i,j)=" + (i, j).to_string, expected, actual)
        )
    )
);

test_tomlval_to_string: TestCase;
test_tomlval_to_string = (
    make_test("test_tomlval_to_string") $ |_|
    assert_equal("string", "\"a\"", v_string("a").to_string);;
    assert_equal("int", "1", v_int(1).to_string);;
    assert_equal("float", "1.000000", v_float(1.0).to_string);;
    assert_equal("bool", "true", v_bool(true).to_string);;
    let array = TomlArray::make $ [v_string("a"), v_int(1)];
    assert_equal("array", "[\"a\", 1]", v_array(array).to_string);;
    let table = TomlTable::empty.insert("a", v_int(1)).insert("b", v_string("c"));
    assert_equal("table", "{a = 1, b = \"c\"}", v_table(table).to_string);;
    pure()
);

test_to_toml_val: TestCase;
test_to_toml_val = (
    make_test("test_to_toml_val") $ |_|
    assert_equal("val", v_int(1), v_int(1).to_toml_val);;
    assert_equal("string", v_string("a"), "a".to_toml_val);;
    assert_equal("int", v_int(1), 1.to_toml_val);;
    assert_equal("float", v_float(1.0), 1.0.to_toml_val);;
    assert_equal("bool", v_bool(true), true.to_toml_val);;
    let array = TomlArray::make $ [v_int(1), v_int(2)];
    assert_equal("array", v_array(array), [1, 2].to_toml_val);;
    pure()
);

test_from_toml_val: TestCase;
test_from_toml_val = (
    make_test("test_from_toml_val") $ |_|
    assert_equal("val ok", ok $ v_int(1), v_int(1).from_toml_val: Result ErrMsg TomlVal);;
    assert_equal("string ok", ok $ "a", v_string("a").from_toml_val: Result ErrMsg String);;
    assert_equal("string err", err $ "not a string", v_int(1).from_toml_val: Result ErrMsg String);;
    assert_equal("int ok", ok $ 1, v_int(1).from_toml_val: Result ErrMsg I64);;
    assert_equal("int err", err $ "not an integer", v_string("a").from_toml_val: Result ErrMsg I64);;
    assert_equal("float ok", ok $ 1.0, v_float(1.0).from_toml_val: Result ErrMsg F64);;
    assert_equal("float err", err $ "not a floating point", v_int(1).from_toml_val: Result ErrMsg F64);;
    assert_equal("bool ok", ok $ true, v_bool(true).from_toml_val: Result ErrMsg Bool);;
    assert_equal("bool err", err $ "not a boolean", v_int(1).from_toml_val: Result ErrMsg Bool);;
    let array = TomlArray::empty.push_back(v_int(1)).push_back(v_int(2));
    assert_equal("array ok", ok $ [1, 2], v_array(array).from_toml_val: Result ErrMsg (Array I64));;
    let array = TomlArray::empty.push_back(v_int(1)).push_back(v_string("a"));
    assert_equal("array err 1", err $ "not an integer", v_array(array).from_toml_val: Result ErrMsg (Array I64));;
    assert_equal("array err 2", err $ "not an array", v_int(1).from_toml_val: Result ErrMsg (Array I64));;
    pure()
);

main: IO ();
main = (
    [
        test_tomltable_simple,
        test_tomltable_indexable_simple,
        test_tomltable_indexable_multi,
        test_tomltable_indexable_multi2,
        test_tomltable_indexable_multi3,
        test_tomlarray_simple,
        test_tomlval_eq,
        test_tomlval_to_string,
        test_to_toml_val,
        test_from_toml_val,
    ]
    .run_test_driver
);
