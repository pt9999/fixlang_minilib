module Main;

import Minilib.IO.FileSystem;
import Minilib.IO.Path;
import Minilib.Terminal.Ncurses;
import Minilib.Trait.Traversable;
import Minilib.Text.StringEx;

namespace StringUtils {
    pad_right: I64 -> U8 -> String -> String;
    pad_right = |result_size, char, str| (
        let str_size = str.get_size;
        if str_size >= result_size {
            str.get_sub(0, result_size)
        };
        let pad_size = result_size - str_size;
        let pad = Array::fill(pad_size, char)._unsafe_to_string;
        str + pad
    );

    pad_left: I64 -> U8 -> String -> String;
    pad_left = |result_size, char, str| (
        let str_size = str.get_size;
        if str_size >= result_size {
            str.get_sub(str_size - result_size, str_size)
        };
        let pad_size = result_size - str_size;
        let pad = Array::fill(pad_size, char)._unsafe_to_string;
        pad + str
    );
}

namespace CustomColorPair {
    cp_selected: ColorPair;
    cp_selected = 10;

    init_custom_color_pairs: IOFail ();
    init_custom_color_pairs = (
        pure();;    // make it lazy
        init_color_pair(cp_selected, white, blue);;
        pure()
    );
}

type FileItem = box struct {
    file_name: String,
    file_path: String,
    stat: FileStat,
};

namespace FileItem {
    get_file_item: String -> String -> IOFail FileItem;
    get_file_item = |dir_path, file_name| (
        let file_path = join_paths([dir_path, file_name]);
        pure $ FileItem {
            file_name: file_name,
            file_path: file_path,
            stat: *stat(file_path)
        }
    );

    get_file_items: String -> IOFail (Array FileItem);
    get_file_items = |dir_path| (
        let file_names = *list_dir(dir_path);
        file_names.map_m(|name| get_file_item(dir_path, name))
    );

    format_file_item: FileItem -> String;
    format_file_item = |item| (
        let name = item.@file_name;
        let stat = item.@stat;
        let name = if stat.is_dir { name + "/" }
        else if stat.st_mode.bit_and(0o100_U32) == 0o100_U32 { name + "*" }
        else { name };
        name.pad_right(20, ' ') +
        stat.st_size.to_string.pad_left(13, ' ')
    );

    get_attr: FileItem -> ChType;
    get_attr = |item| (
        let stat = item.@stat;
        if stat.is_dir { color_pair(cp_blue).bit_or(bold) };
        if stat.st_mode.bit_and(0o100_U32) == 0o100_U32 { color_pair(cp_green).bit_or(bold) };
        color_pair(cp_white)
    );
}

type FileListView = unbox struct {
    window: Window,
    dir_path: String,
    file_items: Array FileItem,
    scroll_top: I64,
    selected_index: I64,
    message: String,
};

namespace FileListView {
    make: Window -> String -> IOFail FileListView;
    make = |window, dir_path| (
        let file_items = *get_file_items(dir_path);
        pure $ FileListView {
            window: window,
            dir_path: dir_path,
            file_items: file_items,
            scroll_top: 0,
            selected_index: -1,
            message: "",
        }
    );

    get_selected_item: FileListView -> Option FileItem;
    get_selected_item = |file_list_view| (
        let file_items = file_list_view.@file_items;
        let selected_index = file_list_view.@selected_index;
        if 0 <= selected_index && selected_index < file_items.get_size {
            some $ file_items.@(selected_index)
        } else {
            none()
        }
    );

    select_item_by_name: String -> FileListView -> IOFail FileListView;
    select_item_by_name = |name, file_list_view| (
        let file_items = file_list_view.@file_items;
        match file_items.find_by(|item| item.@file_name == name) {
            none() => pure $ file_list_view,
            some(index) => pure $ file_list_view.set_selected_index(index)
        }
    );

    show: FileListView -> IOFail FileListView;
    show = |file_list_view| (
        let FileListView {
            dir_path: dir_path,
            file_items: file_items,
            scroll_top: scroll_top,
            selected_index: selected_index,
            message: message,
            window: window
        } = file_list_view;

        window.clear;;

        let (w, h) = *window.get_window_size;
        let visible_lines = max(0, h - 2);
        let scroll_top = if selected_index < 0 { scroll_top }
        else if selected_index < scroll_top || scroll_top + visible_lines <= selected_index {
            max(0, selected_index - visible_lines / 2)
        } else { scroll_top };
        let file_list_view = file_list_view.set_scroll_top(scroll_top);

        loop_m(
            0, |i|
            let file_index = scroll_top + i;
            if i >= visible_lines || file_index >= file_items.get_size { break_m $ () };
            let item = file_items.@(file_index);
            let formatted = item.format_file_item;
            if file_index == selected_index {
                window.set_color_pair(cp_selected, normal)
            } else {
                //window.set_color_pair(cp_white, normal)
                window.set_attr(item.get_attr)
            };;
            window.move_add_str(1, i + 1, formatted);;
            continue_m $ i + 1
        );;
        window.set_color_pair(cp_green, normal);;
        window.draw_border;;
        window.set_color_pair(cp_white, normal);;
        window.move_add_str(2, 0, " " + dir_path + " ");;
        window.move_add_str(2, h - 1, " " + message + " ").when(message != "");;
        pure $ file_list_view
    );

    handle_keys: FileListView -> IOFail FileListView;
    handle_keys = |file_list_view| (
        let FileListView {
            window: window,
            file_items: file_items
        } = file_list_view;
        let key = *window.get_key;
        let file_list_view = file_list_view.set_message(key);
        if key == "q" || key == "Q" || key == "KEY_ESC" {
            throw $ "Quit"
        };
        if key == "j" || key == "KEY_DOWN" {
            pure $ file_list_view.mod_selected_index(|index| min(file_items.get_size - 1, index + 1))
        };
        if key == "k" || key == "KEY_UP" {
            pure $ file_list_view.mod_selected_index(|index| max(0, index - 1))
        };
        if key == "^J" || key == "KEY_ENTER" {
            file_list_view.command_enter
        };
        if key == "^" {
            file_list_view.command_up_directory
        };
        pure $ file_list_view
    );

    command_enter: FileListView -> IOFail FileListView;
    command_enter = |file_list_view| (
        let opt = file_list_view.get_selected_item;
        if opt.is_none {
            pure $ file_list_view
        };
        let item = opt.as_some;
        if ! item.@stat.is_dir {
            pure $ file_list_view
        };
        let dir_path = item.@file_path;
        FileListView::make(file_list_view.@window, dir_path)
    );

    command_up_directory: FileListView -> IOFail FileListView;
    command_up_directory = |file_list_view| (
        let dir_path = file_list_view.@dir_path;
        if dir_path == "/" {
            pure $ file_list_view
        };
        let dir_name = Path::basename(dir_path);
        let parent_path = Path::dirname(dir_path);
        let file_list_view = *FileListView::make(file_list_view.@window, parent_path);
        let file_list_view = *file_list_view.select_item_by_name(dir_name);
        pure $ file_list_view
    );
}

main: IO ();
main = do {
    Ncurses::run $ |win| (
        init_custom_color_pairs;;
        let file_list_view = *FileListView::make(win, *realpath("."));
        loop_m(
            file_list_view, |file_list_view|
            let file_list_view = *file_list_view.show;
            let file_list_view = *file_list_view.handle_keys;
            continue_m $ file_list_view
        )
    )
}.try(eprintln);
