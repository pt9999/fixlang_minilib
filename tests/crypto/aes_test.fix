
module Main;

import Debug;
import Random;

import Minilib.Crypto.AES;
import Minilib.Text.StringEx;
import Minilib.Text.Hex;
import Minilib.Testing.UnitTest;

// Test Cases for AES
// cf. [NIST FIPS 197](https://nvlpubs.nist.gov/nistpubs/FIPS/NIST.FIPS.197-upd1.pdf)

byte_array_from_string_hex: String -> Array U8;
byte_array_from_string_hex = |str| (
    str.split(" ").map(from_string_hex >> as_ok).to_array
);

// Appendix A — Key Expansion Examples
test_key_expansion_128: TestCase;
test_key_expansion_128 = (
    make_test("test_key_expansion_128") $ |_|
    let key = "2b 7e 15 16 28 ae d2 a6 ab f7 15 88 09 cf 4f 3c";
    let key = key.byte_array_from_string_hex;
    let aes = AES::_empty(128);
    let w = aes._key_expansion(key);
    /*
    let _ = *Iterator::range(0, 4 * (aes._get_number_of_rounds + 1)).fold_m(
        (), |_, i|
        println((i, w.@(i).to_string_hex).format("w[{}] = {}")).lift
    );
    */
    let _ = *assert_equal("w[4]", 0xa0fafe17_U32, w.@(4));
    let _ = *assert_equal("w[43]", 0xb6630ca6_U32, w.@(43));
    pure()
);

test_key_expansion_192: TestCase;
test_key_expansion_192 = (
    make_test("test_key_expansion_192") $ |_|
    let key = "8e 73 b0 f7 da 0e 64 52 c8 10 f3 2b 80 90 79 e5 62 f8 ea d2 52 2c 6b 7b";
    let key = key.byte_array_from_string_hex;
    let aes = AES::_empty(192);
    let w = aes._key_expansion(key);
    let _ = *assert_equal("w[6]", 0xfe0c91f7_U32, w.@(6));
    let _ = *assert_equal("w[51]", 0x01002202_U32, w.@(51));
    pure()
);

test_key_expansion_256: TestCase;
test_key_expansion_256 = (
    make_test("test_key_expansion_256") $ |_|
    let key = "60 3d eb 10 15 ca 71 be 2b 73 ae f0 85 7d 77 81 1f 35 2c 07 3b 61 08 d7 2d 98 10 a3 09 14 df f4";
    let key = key.byte_array_from_string_hex;
    let aes = AES::_empty(256);
    let w = aes._key_expansion(key);
    let _ = *assert_equal("w[8]", 0x9ba35411_U32, w.@(8));
    let _ = *assert_equal("w[59]", 0x706c631e_U32, w.@(59));
    pure()
);

// Appendix B — Cipher Example
test_encrypt_decrypt_block: TestCase;
test_encrypt_decrypt_block = (
    make_test("test_encrypt_decrypt_block") $ |_|
    let input = "32 43 f6 a8 88 5a 30 8d 31 31 98 a2 e0 37 07 34";
    let key = "2b 7e 15 16 28 ae d2 a6 ab f7 15 88 09 cf 4f 3c";
    let input = input.byte_array_from_string_hex;
    let key = key.byte_array_from_string_hex;
    let aes = AES::make(key);
    let output = aes.encrypt_block(input);
    //let _ = *println(output.to_string_hex).lift;
    let output_expect = "3925841d02dc09fbdc118597196a0b32";
    let _ = *assert_equal("output", output_expect, output.to_string_hex);
    let input2 = aes.decrypt_block(output);
    //let _ = *println(input2.to_string_hex).lift;
    assert_equal("decrypt", input, input2)
);

main: IO ();
main = (
    [
        test_key_expansion_128,
        test_key_expansion_192,
        test_key_expansion_256,
        test_encrypt_decrypt_block,
        TestCase::empty
    ]
    .run_test_driver
);
