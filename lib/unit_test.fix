module UnitTest;

import Debug;

//  IOFail (ok_count, ng_count)
type TestCase = IOFail (I64, I64);

run_test_driver: Array TestCase -> IO ();
run_test_driver = |tests| (
    do {
        let (ok, ng) = *(tests.run_tests);
        let _ = *(lift $ println ("OK: " + ok.to_string + " NG: " + ng.to_string));
        pure()
    }.try(|err| println ("FATAL ERROR: " + err))
);

run_tests: Array (TestCase) -> TestCase;
run_tests = |tests| (
    tests.to_iter.fold_m(
        (0, 0), |(ok_total, ng_total), test|
        let (ok, ng) = *test;
        pure $ (ok_total + ok, ng_total + ng)
    )
);
    

make_test: String -> IOFail () -> TestCase;
make_test = |test_name, body| (
    eval *(lift $ println ("=== " + test_name + " ==="));
    do {
        let _ = *body;
        eval *(lift $ println ("[OK] " + test_name));
        pure $ (1, 0)
    }.try(|err| 
        eval *(println("error: " + err));
        eval *(println("[NG] " + test_name));
        pure $ (0, 1)
    ).lift
);


assert_true : String -> Bool -> IOFail ();
assert_true = |msg, b| (
    if !b {
        let msg = "assert_true: " + msg;
        throw $ msg
    };
    pure()
);

assert_equal : [a: Eq, a: ToString] String -> a -> a -> IOFail ();
assert_equal = |msg, lhs, rhs| (
    if lhs != rhs {
        let msg = "assert_equal: " + msg + ": lhs="+lhs.to_string+" rhs="+rhs.to_string;
        eval debug_println("throwing: " + msg);
        throw $ msg
    };
    pure()
);


