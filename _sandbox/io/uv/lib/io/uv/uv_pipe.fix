module Minilib.IO.Uv.UvPipe;

import Minilib.IO.Uv;
import Minilib.IO.Uv.UvStream;
import Minilib.Monad.IO;

//-----------------------------------------------------------------
// UvPipe
//-----------------------------------------------------------------

type UvPipeTag = unbox struct {};
impl UvPipeTag: IsUvStreamTag {}
type UvPipe = UvHandle UvPipeTag;

namespace UvPipe {
    // uv_pipe_t* を確保して初期化し、UvPipeを作成する。
    init: UvLoop -> IOFail UvPipe;
    init = |loop| (
        loop.borrow_ptr_m(|p_loop|
            let p_pipe = *FFI_CALL_IO[Ptr minilib_uv_pipe_init(Ptr), p_loop].lift;
            if p_pipe == nullptr { throw $ "minilib_uv_pipe_init failed!" };
            from_ptr(p_pipe).lift
        )
    );

    // Open an existing file descriptor as a pipe.
    open: UvFile -> UvPipe -> IOFail ();
    open = |fd, pipe| (
        pipe.borrow_ptr_m(|p_pipe|
            FFI_CALL_IO[CInt minilib_uv_pipe_open(Ptr, CInt), p_pipe, fd]
            ._check_err
        )
    );

    make_pipe_pair: UvLoop -> IOFail (UvPipe, UvPipe);
    make_pipe_pair = |loop| (
        let fds: Array UvFile = Array::fill(2, 0.to_CInt);
        let (fds, ()) = *fds.mutate_boxed_m(|p_fds|
            FFI_CALL_IO[CInt minilib_uv_make_pipe_pair(Ptr), p_fds]
            ._check_err
        );
        let read_pipe = *UvPipe::init(loop);
        let write_pipe = *UvPipe::init(loop);
        read_pipe.open(fds.@(0));;
        write_pipe.open(fds.@(1));;
        pure $ (read_pipe, write_pipe)
    );
}
